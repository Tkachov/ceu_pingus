#ifndef DEMO_SESSION_CEU
#define DEMO_SESSION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/gui/gui_manager.ceu"
#include "../../engine/screen/screen.ceu"
#include "../iworld.ceu"
#include "../global_interface.ceu"
#include "../components/button_panel.ceu"
#include "../components/pingus_counter.ceu"
#include "../components/playfield.ceu"
#include "../components/smallmap.ceu"
#include "../server.ceu"

native/pre do
    ##include "pingus/screens/demo_session.hpp"
    ##include "engine/gui/gui_manager.hpp"
    ##include "engine/gui/surface_button.hpp"
    ##include "engine/screen/screen_manager.hpp"
    ##include "pingus/pingus_demo.hpp"
    ##include "pingus/server.hpp"
    ##include "util/ceu_utils.h"
    ##include "util/log.hpp"
    ##define new_GUI_Component() new GUI::Component()
    ##define new_RectComponent(r) new GUI::RectComponent(r)
    ##define new_Rect(v, s) new Rect(v, s)
    ##define new_SceneContext(rect) new SceneContext(rect)
    ##define new_GraphicContextState new GraphicContextState
    ##define new_Server(a) new Server(a)    
    ##define ScreenManager__instance() ScreenManager::instance()
    ##define new_BButton new BButton
    ##define std__bind(f, p) std::bind(f, p)    
    ##define std__make_shared_DemoSession std::make_shared<DemoSession>
end

native @pure _new_PingusCounter(), _new_GUI_Component(), _new_RectComponent(), 
             _new_Rect(), _new_SceneContext(), _new_GraphicContextState(),
             _new_Server(), _ScreenManager__instance(), _addr(),
             _new_BButton(), _std__bind(), _std__make_shared_DemoSession();

input _ComponentUpdatePackage* DEMO_SESSION_UPDATE_SERVER;
input _DemoSession* DEMO_SESSION_UPDATE;
input _DemoSession* DELETE_DEMO_SESSION;
input _DemoSession* RESTART_DEMO_SESSION;
input _Vector2i* DEMO_SESSION_ON_SCROLLER_MOVE;

class DemoSession with
    interface Screen;
    event void update_demo;
do
    var _DemoSession&? demo_session = (_DemoSession*)&me;

    var GUIManager gui_manager with
        this.me = _new_GUI__RectComponent(_Rect(0, 0, _Display__get_width(), _Display__get_height()));
        this.drawing_context = _new_DrawingContext(_Rect(0, 0, _Display__get_width(), _Display__get_height()), false);
    end;

    var ScreenFramework sf with
        this.screen = outer;
        this.gui_manager = &gui_manager;
    end;

    var Server server with
        this.me = _new_Server(demo_session.plf);        
        demo_session.server = _PTR2REF(this.me);
    end;

    var PingusCounter pcounter with
        this.me = _new_GUI_Component();
        this.me.ceu = &this;
        this.server = server;
    end;

    var Playfield playfield with
        var _Rect&? rect = _new_Rect(
                                _Vector2i(
                                    _Math__max((demo_session.size.width - global:world:get_width())/2, 0),
                                    _Math__max((demo_session.size.height - global:world:get_height())/2, 0)
                                ),
                                _Size(
                                    _Math__min(demo_session.size.width, global:world:get_width()),
                                    _Math__min(demo_session.size.height, global:world:get_height())
                                )
                            );
        
        this.me = _new_RectComponent(_deref(rect));
        this.me.ceu = &this;
        this.scene_context = _new_SceneContext(_deref(rect));
        this.state = _new_GraphicContextState(rect.get_width(), rect.get_height());
        this.server = server;
        this.session = null;

        _delete_obj(rect);
    end;

    var SmallMap small_map with
        this.rect = _new_RectComponent(_Rect(_Vector2i(5, demo_session.size.height - 105), _Size(175, 100)));
        this.me = this.rect;
        this.me.ceu = &this;
        this.playfield = playfield;
    end;

    var ButtonPanel button_panel with
        this.me = _new_RectComponent(_Rect());
        this.me.ceu = &this;
        this.server = server;
    end;

    do
        var Vector2i bp_pos;
        bp_pos.x = 0;
        bp_pos.y = (demo_session.size.height - 100)/2;
        button_panel.set_pos(bp_pos);
    end

    gui_manager.add(&pcounter.me);
    gui_manager.add(&button_panel.me);
    gui_manager.add(&playfield.me);
    gui_manager.add(&small_map.rect);

    demo_session.fastforward_button = _new_BButton(32+50, 32, "core/demo/fastforward",
                                                   _std__bind({&DemoSession::on_fast_forward_press}, &demo_session),
                                                   _std__bind({&DemoSession::is_fast_forward}, &demo_session));

    demo_session.pause_button = _new_BButton(32, 32, "core/demo/pause",
                                             _std__bind({&DemoSession::on_pause_press}, &demo_session),
                                             _std__bind({&DemoSession::is_pause}, &demo_session));

    demo_session.restart_button = _new_BButton(demo_session.size.width - 32 - 48, 32, "core/demo/reload",
                                               _std__bind({&DemoSession::restart}, &demo_session));

    gui_manager.add(demo_session.fastforward_button);
    gui_manager.add(demo_session.pause_button);
    gui_manager.add(demo_session.restart_button);

    par/or do
        every delta in global:update do
            if call/rec server.is_finished() then
                _ScreenManager__instance():pop_screen();
            else
                //original FIXME: Duplicate all timing code here?!
                if not demo_session.pause then
                    if demo_session.fast_forward then
                        loop i in 4 do
                            emit update_demo;
                            { ceu_utils::async_emit(CEU_IN_SERVER_UPDATE); };
                        end
                    else
                        emit update_demo;
                        { ceu_utils::async_emit(CEU_IN_SERVER_UPDATE); };
                    end
                end
            end
        end
    with
        every update_demo do
            loop do
                if demo_session.events.empty() or demo_session.events.back().time_stamp != call/rec server.get_time() then
                    break;
                end

                emit server.e_EVENT_SEND => _addr(demo_session.events.back());
                demo_session.events.pop_back();
            end

            // Check for unexpected things (might happen if the demo file is broken)
            if not demo_session.events.empty() and demo_session.events.back().time_stamp < call/rec server.get_time() then
                _log_info("DemoPlayer Bug: We missed a timestamp: %1%", demo_session.events.back().time_stamp);
            end
        end
    with
        every gc in draw_background do
            var _GUI_RectComponent* rect = (_GUI_RectComponent*)&playfield.me;

            if rect:get_rect() != _Rect(_Vector2i(0,0), _Size(gc:get_width(), gc:get_height())) then
                // Draw a black border around the playfield when the playfield is smaller then the screen                
                // top
                gc:draw_fillrect(_Rect(0, 0, gc:get_width(), rect:get_rect().top), _Color(0, 0, 0));
                // bottom
                gc:draw_fillrect(_Rect(0, rect:get_rect().bottom, gc:get_width(), gc:get_height()), _Color(0, 0, 0));
                // left
                gc:draw_fillrect(_Rect(0, rect:get_rect().top, rect:get_rect().left, rect:get_rect().bottom), _Color(0, 0, 0));
                // right
                gc:draw_fillrect(_Rect(rect:get_rect().right, rect:get_rect().top, gc:get_width(), rect:get_rect().bottom), _Color(0, 0, 0));
            end
        end 
    with
        every on_escape_press do            
            _log_info("Escape Pressed");
            emit server.e_FINISH_EVENT;
            _ScreenManager__instance():pop_screen();            
        end
    with
        every pos in DEMO_SESSION_ON_SCROLLER_MOVE do
            //original FIXME: Rounding considered evil?
            playfield.scroll(-pos:x, -pos:y);
            //well, I think it isn't
        end
    with
        var _DemoSession* e = await RESTART_DEMO_SESSION until (e == &demo_session);

        //await 10ms;                
        _ScreenManager__instance():replace_screen(_std__make_shared_DemoSession(demo_session.pathname));

        e = await DELETE_DEMO_SESSION until (e == &demo_session);
    with
        every (width, height) in resize do
            ((_GUI_RectComponent*)&playfield.me):set_rect(
                _Rect(
                    _Vector2i(
                        _Math__max((width  - global:world:get_width())/2,  0),
                        _Math__max((height - global:world:get_height())/2, 0)
                    ), 
                    _Size(
                        _Math__min(width,  global:world:get_width()),
                        _Math__min(height, global:world:get_height())
                    )
                )
            );

            demo_session.fastforward_button:set_pos(32+50, 32);
            demo_session.pause_button:set_pos(32, 32);
            demo_session.restart_button:set_pos(width - 32 - 48, 32);

            small_map.rect.set_rect(_Rect(_Vector2i(5, height - 105), _Size(175, 100)));
        end
    with
        var _DemoSession* e = await DELETE_DEMO_SESSION until (e == &demo_session);
        emit server.e_DELETE;
    end
end

#endif
