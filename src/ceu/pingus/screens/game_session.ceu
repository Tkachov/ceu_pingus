#ifndef GAME_SESSION_CEU
#define GAME_SESSION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/screen/screen.ceu"
#include "../../engine/gui/gui_manager.ceu"
#include "../../math/math.ceu"
#include "../iworld.ceu"
#include "../global_interface.ceu"
#include "../components/armageddonbutton.ceu"
#include "../components/button_panel.ceu"
#include "../components/forwardbutton.ceu"
#include "../components/pausebutton.ceu"
#include "../components/pingus_counter.ceu"
#include "../components/playfield.ceu"
#include "../components/smallmap.ceu"
#include "../components/time_display.ceu"
#include "../server.ceu"
#include "igame_session.ceu"

native/pre do
    ##include "engine/screen/screen_manager.hpp"
    ##include "engine/display/display.hpp"
    ##include "engine/gui/gui_manager.hpp"
    ##include "pingus/globals.hpp"
    ##include "pingus/savegame_manager.hpp"
    ##include "pingus/screens/game_session.hpp"
    ##include "pingus/screens/result_screen.hpp"

    ##include "engine/screen/screen_manager.hpp"
    ##include "pingus/server.hpp"
    ##include "engine/sound/sound.hpp" 

    ##include "engine/display/graphic_context_state.hpp"
    ##include "engine/gui/rect_component.hpp"
    ##include "util/ceu_utils.h"
    ##include "util/log.hpp"

    ##define new_GUI_Component() new GUI::Component()
    ##define new_RectComponent(r) new GUI::RectComponent(r)
    ##define new_Rect(v, s) new Rect(v, s)
    ##define new_SceneContext(rect) new SceneContext(rect)
    ##define new_GraphicContextState new GraphicContextState
    ##define new_Server(a) new Server(a)  
    ##define new_TimeDisplay(s) new TimeDisplay(s)
    ##define new_Result() new Result()
    ##define new_Savegame new Savegame
    ##define SavegameManager__instance() SavegameManager::instance()
    ##define ScreenManager__instance() ScreenManager::instance()
    ##define std__make_shared_ResultScreen std::make_shared<ResultScreen>
    ##define Savegame__ACCESSIBLE Savegame::ACCESSIBLE
    ##define Savegame__FINISHED Savegame::FINISHED
    ##define globals__game_speed globals::game_speed
    ##define globals__fast_forward_time_scale globals::fast_forward_time_scale
    ##define toSStatus(x) (Savegame::S_Status)x
    ##define Display__get_width() Display::get_width()
    ##define Display__get_height() Display::get_height()
    ##define Input__ScrollEvent Input::ScrollEvent

    ##define Input__BUTTON_EVENT_TYPE Input::BUTTON_EVENT_TYPE
    ##define Input__BUTTON_PRESSED Input::BUTTON_PRESSED
    ##define Input__ACTION_1_BUTTON Input::ACTION_1_BUTTON
    ##define Input__ACTION_10_BUTTON Input::ACTION_10_BUTTON
    ##define Input__ACTION_DOWN_BUTTON Input::ACTION_DOWN_BUTTON
    ##define Input__ACTION_UP_BUTTON Input::ACTION_UP_BUTTON
    ##define Input__SCROLLER_EVENT_TYPE Input::SCROLLER_EVENT_TYPE
    ##define Input__POINTER_EVENT_TYPE Input::POINTER_EVENT_TYPE
    ##define Input__AXIS_EVENT_TYPE Input::AXIS_EVENT_TYPE
    ##define Input__KEYBOARD_EVENT_TYPE Input::KEYBOARD_EVENT_TYPE
    ##define Input__TEXT_INPUT_EVENT_TYPE Input::TEXT_INPUT_EVENT_TYPE    
end

native @pure _new_PingusCounter(), _new_GUI_Component(), _new_RectComponent(), _new_Server(),
             _new_Rect(), _new_SceneContext(), _new_GraphicContextState(), _new_TimeDisplay(), _new_Result(), _new_Savegame(),
             _SavegameManager__instance(), _ScreenManager__instance(), _get_demostream(),
             _std__make_shared_ResultScreen(), _toSStatus(), _Display__get_width(), _Display__get_height();

input _GameSession* DELETE_GAME_SESSION;

class GameSession with
    interface Screen;
    interface IGameSession;
do
    var _GameSession&? game_session = _PTR2REF((_GameSession*)&me);
    var GUIManager gui_manager with
        this.me = _new_GUI__RectComponent(_Rect(0, 0, _Display__get_width(), _Display__get_height()));
        this.drawing_context = _new_DrawingContext(_Rect(0, 0, _Display__get_width(), _Display__get_height()), false);
    end;
    var ScreenFramework sf with
        this.screen = outer;
        this.gui_manager = &gui_manager;
    end;

    var Server server with
        this.me = _new_Server(game_session.plf);
        this.me:demostream = _get_demostream(this.me:plf);
    end;

    var ButtonPanel button_panel with
        this.me = _PTR2REF(_new_RectComponent(_Rect()));
        this.me.ceu = &this;
        this.server = server;
    end;

    do
        var Vector2i bp_pos;
        bp_pos.x = 0;
        bp_pos.y = (game_session.size.height - 150)/2;
        button_panel.set_pos(bp_pos);
    end

    var PingusCounter pcounter with
        this.me = _new_GUI_Component();
        this.me.ceu = &this;
        this.server = server;
    end;

    var Playfield playfield with
        var _Rect&? rect = _new_Rect(
                                _Vector2i(
                                    _Math__max((_Display__get_width() - global:world:get_width())/2, 0),
                                    _Math__max((_Display__get_height() - global:world:get_height())/2, 0)
                                ),
                                _Size(
                                    _Math__min(_Display__get_width(), global:world:get_width()),
                                    _Math__min(_Display__get_height(), global:world:get_height())
                                )
                            );
        
        this.me = _PTR2REF(_new_RectComponent(_deref(rect)));
        this.me.ceu = &this;
        this.scene_context = _new_SceneContext(_deref(rect));
        this.state = _new_GraphicContextState(rect.get_width(), rect.get_height());
        this.server = server;
        this.session = &outer;

        _delete_obj(rect);
    end;

    var SmallMap small_map with
        this.rect = _PTR2REF(_new_RectComponent(_Rect(_Vector2i(5, game_session.size.height - 105), _Size(175, 100))));
        this.me = _PTR2REF(this.rect);
        this.me.ceu = &this;    
        this.playfield = playfield;
    end;

    var ArmageddonButton armageddon_button with
        this.me = _PTR2REF(_new_RectComponent(_Rect(_Vector2i(_Display__get_width() - 40, _Display__get_height()-62), _Size(38, 60))));
        this.me.ceu = &this;
        this.server = server;
    end;

    var ForwardButton forward_button with
        this.me = _PTR2REF(_new_RectComponent(_Rect(_Vector2i(_Display__get_width() - 40*2, _Display__get_height()-62), _Size(38, 60))));
        this.me.ceu = &this;
        this.session = outer;
    end;

    var PauseButton pause_button with
        this.me = _PTR2REF(_new_RectComponent(_Rect(_Vector2i(_Display__get_width() - 40*3, _Display__get_height()-62), _Size(38, 60))));
        this.me.ceu = &this;
        this.session = outer;
    end;
    
    var TimeDisplay time_display with
        this.me = _new_GUI_Component();
        this.me.ceu = &this;
        this.server = server;
    end;

    gui_manager.add(&playfield.me);
    gui_manager.add(&button_panel.me);
    gui_manager.add(&pcounter.me);
    gui_manager.add(&small_map.rect);
    gui_manager.add(&time_display.me);
    gui_manager.add(&armageddon_button.me);
    gui_manager.add(&forward_button.me);
    gui_manager.add(&pause_button.me);

    var int world_delay = 0;

    var bool pause = false;
    var bool fast_forward = false;
    var bool single_step = false;

    function (bool value) => void set_fast_forward do    
        fast_forward = value;
        if fast_forward then        
            pause = false;
        end
    end
    
    function (bool value) => void set_pause do
        pause = value;
        if pause then
            fast_forward = false;
        end
    end

    function (void) => int get_action_name do
        return button_panel.get_action_name();
    end

    function (void) => bool get_fast_forward do    
        return fast_forward;
    end

    function (void) => bool get_pause do
        return pause;
    end

    par/or do
        every delta in global:update do
            if call/rec server.is_finished() then
                var IPinguHolder* pingu_holder = _PTR2REF(global:world:get_pingus());
                var _Result&? result = _new_Result();

                finalize result.plf    = server.get_plf(); with end

                result.saved  = pingu_holder:get_number_of_exited();
                result.killed = pingu_holder:get_number_of_killed();
                finalize result.total  = server.get_plf().get_number_of_pingus(); with end

                finalize result.needed = server.get_plf().get_number_to_save(); with end

                finalize result.max_time  = server.get_plf().get_time(); with end
                result.used_time = call/rec server.get_time();

                var int rs = _Savegame__ACCESSIBLE;
                if result.saved >= result.needed then
                    rs = _Savegame__FINISHED;
                end

                // Write the savegame
                var _Savegame&? savegame = _new_Savegame(result.plf.get_resname(),
                                                         _toSStatus(rs),
                                                         result.used_time,
                                                         result.saved);
                _SavegameManager__instance():store(_deref(savegame));

                if game_session.show_result_screen then
                    _ScreenManager__instance():replace_screen(_std__make_shared_ResultScreen(_deref(result)));
                else
                    _ScreenManager__instance():pop_screen();
                end

                _delete_obj(result);
            else

                // how much time we have to account for while doing world updates
                var int time_passed = (int)(delta * 1000) + world_delay;
                // how much time each world update represents
                var int update_time = _globals__game_speed;

                // update the world (and the objects in it) in constant steps to account
                // for the time the previous frame took

                // invariant: world_updates - the number of times the world
                // has been updated during this frame
                var int world_updates = 0;
                loop do
                    if (world_updates+1)*update_time > time_passed then
                        break;
                    end

                    if not pause or single_step then
                        single_step = false;

                        if fast_forward then
                            loop i in _globals__fast_forward_time_scale do
                                { ceu_utils::async_emit(CEU_IN_SERVER_UPDATE); };
                            end
                        else
                            { ceu_utils::async_emit(CEU_IN_SERVER_UPDATE); };
                        end
                    end

                    world_updates = world_updates + 1;
                end
                // save how far behind is the world compared to the actual time
                // so that we can account for that while updating in the next frame
                world_delay = time_passed - (world_updates*update_time);
            end
        end
    with
        every ev in update_event do
            //log_info("Events: " << event.get_type ());

            if ev:type == _Input__BUTTON_EVENT_TYPE then
                if ev:button.state == _Input__BUTTON_PRESSED then              
                    if  ev:button.name >= _Input__ACTION_1_BUTTON
                    and ev:button.name <= _Input__ACTION_10_BUTTON then
                        button_panel.set_button(ev:button.name - _Input__ACTION_1_BUTTON);                    
                    else/if ev:button.name == _Input__ACTION_DOWN_BUTTON then                    
                        button_panel.next_action();                    
                    else/if ev:button.name == _Input__ACTION_UP_BUTTON then                    
                        button_panel.previous_action();
                    end
                end
            else/if ev:type == _Input__SCROLLER_EVENT_TYPE then
                playfield.scroll(-ev:scroll.x_delta, -ev:scroll.y_delta);
            else/if ev:type == _Input__POINTER_EVENT_TYPE then // Ignore, is handled in GUIScreen
            else/if ev:type == _Input__AXIS_EVENT_TYPE then
                // ???
                //process_axis_event (event.axis);
            else/if ev:type == _Input__KEYBOARD_EVENT_TYPE then
            else/if ev:type == _Input__TEXT_INPUT_EVENT_TYPE then
            else
                // unhandled event
                _log_info("unhandled event: %1%", ev:type) finalize with end;
            end
        end        
    with
        every on_startup do
            if _globals__developer_mode then
                _log_info("Starting Music: %1%", server.get_plf().get_music());
            end

            if server.get_plf().get_music() == "none" or server.get_plf().get_music().empty() then
                _Sound__PingusSound__stop_music();
            else
                _Sound__PingusSound__play_music(server.get_plf().get_music());
            end
        end
    with
        every on_escape_press do
            emit server.e_FINISH_EVENT;            
        end
    with
        every on_armageddon_press do
            emit server.e_ARMAGEDDON;            
        end
    with
        every on_pause_press do
            set_pause(not get_pause());            
        end
    with
        every on_single_step_press do
            set_pause(true);
            single_step = true;
        end    
    with
        every on_fast_forward_press do
            if get_pause() then
                set_pause(false);
            else
                set_fast_forward(true);
            end
        end
    with
        every on_fast_forward_release do
            set_fast_forward(false);
        end
    with
        every gc in draw_background do
            var _GUI_RectComponent* rect = (_GUI_RectComponent*)&playfield.me;

            if rect:get_rect() != _Rect(_Vector2i(0,0), _Size(_Display__get_width(), _Display__get_height())) then
                // Draw a black border around the playfield when the playfield is smaller then the screen                
                // top
                gc:draw_fillrect(_Rect(0, 0, _Display__get_width(), rect:get_rect().top), _Color(0, 0, 0));
                // bottom
                gc:draw_fillrect(_Rect(0, rect:get_rect().bottom, _Display__get_width(), _Display__get_height()), _Color(0, 0, 0));
                // left
                gc:draw_fillrect(_Rect(0, rect:get_rect().top, rect:get_rect().left, rect:get_rect().bottom), _Color(0, 0, 0));
                // right
                gc:draw_fillrect(_Rect(rect:get_rect().right, rect:get_rect().top, _Display__get_width(), rect:get_rect().bottom), _Color(0, 0, 0));
            end
        end
    with
        every package in SCREEN_RESIZE do
            if package:screen != &game_session then
                continue;
            end

            ((_GUI_RectComponent*)&playfield.me):set_rect(
                _Rect(
                    _Vector2i(
                        _Math__max((game_session.size.width - global:world:get_width())/2,  0),
                        _Math__max((game_session.size.height - global:world:get_height())/2, 0)
                    ),
                    _Size(
                        _Math__min(game_session.size.width, global:world:get_width()),
                        _Math__min(game_session.size.height, global:world:get_height())
                    )
                )
            );

            ((_GUI_RectComponent*)&armageddon_button.me):set_rect(_Rect(_Vector2i(game_session.size.width - 40, game_session.size.height - 62), _Size(38, 60)));
            ((_GUI_RectComponent*)&forward_button.me):set_rect(_Rect(_Vector2i(game_session.size.width - 40*2, game_session.size.height - 62), _Size(38, 60)));
            ((_GUI_RectComponent*)&pause_button.me):set_rect(_Rect(_Vector2i(game_session.size.width - 40*3, game_session.size.height - 62), _Size(38, 60)));
            small_map.rect.set_rect(_Rect(_Vector2i(5, game_session.size.height - 105), _Size(175, 100)));

            var Vector2i bp_pos;
            bp_pos.x = 0;
            bp_pos.y = (game_session.size.height - 150)/2;
            button_panel.set_pos(bp_pos);
        end
    with
        every move in on_action_axis_move do
            if move > 0 then
                button_panel.next_action();
            else/if move < 0 then
                button_panel.previous_action();
            end
        end
    with
        var _GameSession* e = await DELETE_GAME_SESSION until (e == &game_session);
        emit server.e_DELETE;
    end
end

#endif
