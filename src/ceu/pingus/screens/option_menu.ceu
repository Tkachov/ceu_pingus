#ifndef OPTION_MENU_CEU
#define OPTION_MENU_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/gui/igui_component.ceu"
#include "../../engine/gui/gui_manager.ceu"
#include "../../engine/gui/surface_button.ceu"
#include "../../engine/screen/screen.ceu"
#include "../../engine/sound/sound.ceu"
#include "../../math/math.ceu"
#include "../components/check_box.ceu"
#include "../components/choice_box.ceu"
#include "../components/label.ceu"
#include "../components/slider_box.ceu"
#include "../global_interface.ceu"
#include "../iworld.ceu"

native/pre do
    ##include "engine/display/display.hpp"
    ##include "engine/display/graphic_context_state.hpp"
    ##include "engine/display/sprite.hpp"
    ##include "engine/gui/gui_manager.hpp"
    ##include "engine/gui/rect_component.hpp"
    ##include "engine/gui/surface_button.hpp"
    ##include "engine/screen/gui_screen.hpp"
    ##include "engine/screen/screen_manager.hpp"
    ##include "engine/sound/sound.hpp"
    ##include "pingus/config_manager.hpp"
    ##include "pingus/fonts.hpp"
    ##include "pingus/game_time.hpp"
    ##include "pingus/gettext.h"
    ##include "pingus/globals.hpp"
    ##include "pingus/savegame_manager.hpp"
    ##include "pingus/screens/game_session.hpp"
    ##include "pingus/screens/option_menu.hpp"
    ##include "pingus/screens/result_screen.hpp"
    ##include "pingus/server.hpp"
    ##include "pingus/string_format.hpp"
    ##include "tinygettext/dictionary_manager.hpp"
    ##include "tinygettext/language.hpp"
    ##include "util/log.hpp"
    ##include "util/string_util.hpp"
    ##include "util/system.hpp"
    ##include <boost/signals2.hpp>
    ##include <map>
    ##include <sstream>
    ##include <vector>

    ##define new_GUI_Component() new GUI::Component()
    ##define ScreenManager__instance() ScreenManager::instance()
    ##define std__make_shared_GameSession std::make_shared<GameSession>
    ##define Display__get_width() Display::get_width()
    ##define Display__get_height() Display::get_height()
    ##define SU2str(x) StringUtil::to_string(x)
    ##define Fonts__chalk_large Fonts::chalk_large
    ##define Fonts__chalk_normal Fonts::chalk_normal
    ##define Fonts__chalk_small Fonts::chalk_small
    ##define StringFormat__break_line StringFormat::break_line

    ##define new_Pathname new Pathname

    ##define System__get_userdir() System::get_userdir()
    ##define Pathname__SYSTEM_PATH Pathname::SYSTEM_PATH

    extern tinygettext::DictionaryManager dictionary_manager;

    ##define std__vector_Option std::vector<Option>
    ##define new_Vector_Option new std::vector<Option>

    ##define resolution_string(s, w, h, rr) { std::ostringstream ostr; ostr << w << "x" << h << "@" << rr; s = ostr.str(); }

    ##define std__vector_SDL_DisplayMode std::vector<SDL_DisplayMode>
    ##define new_Display__get_fullscreen_video_modes() new std::vector<SDL_DisplayMode>(Display::get_fullscreen_video_modes());
    ##define new_Vector_Language new std::vector<tinygettext::Language>
    ##define copy_language_set(v) { std::set<tinygettext::Language> st = dictionary_manager.get_languages(); v->insert(v->begin(), st.begin(), st.end()); }
    ##define std__vector_Language std::vector<tinygettext::Language>
    ##define tinygettext__Language__from_name tinygettext::Language::from_name
    ##define new_DrawingContext new DrawingContext
end

native @pure _new_GUI_Component(), _ScreenManager__instance(), _std__make_shared_GameScreen(),
             _Display__get_width(), _Display__get_height(), _System__get_userdir(), _new_Pathname(),
             _SU2str(), _StringFormat__break_line(), _new_Vector_Option(), _sscanf(), _copy_language_set(),
             _new_Display__get_fullscreen_video_modes(), _new_Vector_Language(), _tinygettext__Language__from_name(),
             _new_DrawingContext();

interface IOptionMenu with
    function (void) => void close_screen;
end

class OptionMenuCloseButton with
    interface IGUI_Component;
    var IOptionMenu& parent;

    function @rec (int x, int y) => void set_pos;
do
    var SurfaceButton sb with
        this.me = outer.me;
        this.pos.x = _Display__get_width()/2 + 245;
        this.pos.y = _Display__get_height()/2 + 150;
    end;

    call/rec sb.load("core/start/ok", "core/start/ok_clicked", "core/start/ok_hover");

    var PrimaryButtonProxy p with this.gui = outer; end;
    var PointerProxy pp with this.gui = outer; end;
    var EventProxy ep with this.gui = outer; end;

    function @rec (int x, int y) => void set_pos do
        sb.set_pos(x, y);
    end

    par do
        every mouse_click do
            parent.close_screen();
            _Sound__PingusSound__play_sound("yipee");            
        end
    with
        every mouse_enter do
            _Sound__PingusSound__play_sound("tick");
        end
    end
end

////////////////////////////////////////////////

#define MakeCheckBox(varname) \
    var CheckBox varname with \
        this.me = _new_GUI__RectComponent(); \
        this.me.ceu = &this; \
    end;

#define MakeSliderBox(varname, steps) \
    var SliderBox varname with \
        this.me = _new_GUI__RectComponent(); \
        this.me.ceu = &this; \
        this.value = steps; \
        this.m_steps = steps; \
    end;

#define MakeChoiceBox(varname) \
    var ChoiceBox varname with \
        this.me = _new_GUI__RectComponent(); \
        this.me.ceu = &this; \
    end;

#define AddItem(labelname, str, control, type) \
    var Label labelname with \
        this.me = _new_GUI__RectComponent(); \
        this.me.ceu = &this; \
        this.label = _new_String(__(str)); \
    end; \
    add_item((_GUI_RectComponent*)&labelname.me, (_GUI_RectComponent*)&control.me, type);

class OptionMenu with
    interface IOptionMenu;
    var _OptionMenu* me;
do
    var _OptionMenu&? option_menu = _PTR2REF((_OptionMenu*)me);
    var GUIManager gui_manager with
        this.me = _new_GUI__RectComponent(_Rect(0, 0, _Display__get_width(), _Display__get_height()));
        this.drawing_context = _new_DrawingContext(_Rect(0, 0, _Display__get_width(), _Display__get_height()), false);
    end;

    var OptionMenuCloseButton ok_button with
        this.me = _new_GUI_Component();
        this.me.ceu = &this;
        this.parent = outer;        
    end;
    gui_manager.add(&ok_button.me);

    var int x_pos = 0;
    var int y_pos = 0;

    var _std__vector_Option&? options = _new_Vector_Option();
    finalize with _delete_obj(options); end

    MakeChoiceBox(resolution_box);
    do
        var _std__vector_SDL_DisplayMode&? resolutions = _new_Display__get_fullscreen_video_modes();
        var Size fullscreen;
        _readSize(fullscreen, _config_manager.get_fullscreen_resolution());

        var int choice = resolutions.size()-1;
        var _std__string&? str = _new_String();
        loop i in resolutions.size() do        
            // add resolution to the box
            _resolution_string(_deref(str), resolutions.at(i).w, resolutions.at(i).h, resolutions.at(i).refresh_rate);
            resolution_box.add_choice(str.c_str());

            //original FIXME: ignoring refresh_rate
            if fullscreen.width == resolutions.at(i).w and fullscreen.height == resolutions.at(i).h then
                choice = i;
            end
        end

        resolution_box.set_current_choice(choice);
        _delete_obj(resolutions);
        _delete_obj(str);
    end

    MakeChoiceBox(renderer_box);
    renderer_box.add_choice("sdl");
    renderer_box.add_choice("delta");
    renderer_box.add_choice("opengl");

    if _config_manager.get_renderer() == _SDL_FRAMEBUFFER then renderer_box.set_current_choice(0);
    else/if _config_manager.get_renderer() == _DELTA_FRAMEBUFFER then renderer_box.set_current_choice(1);
    else/if _config_manager.get_renderer() == _OPENGL_FRAMEBUFFER then renderer_box.set_current_choice(2);
    else _assert(not "unknown renderer type"); end

    option_menu.m_language = _PTR2REF(_dictionary_manager.get_language());  
    
    MakeChoiceBox(language_box);
    do
        var _std__vector_Language&? langs = _new_Vector_Language();
        _copy_language_set(langs);

        // English is the default language, thus it's not in the list of
        // languages returned by tinygettext and we have to add it manually
        langs.push_back(_tinygettext__Language__from_name("en"));

        _std__sort(langs.begin(), langs.end(), _LanguageSorter());

        loop i in langs.size() do        
            finalize option_menu.m_language_map[langs.at(i).get_name()] = langs.at(i); with end;
            language_box.add_choice(langs.at(i).get_name().c_str());

            if option_menu.m_language == langs.at(i) then
                language_box.set_current_choice(i);
            end
        end

        _delete_obj(langs);
    end

    function (_GUI_RectComponent* label_component, _GUI_RectComponent* control, int type) => void add_item do
        //type == 1 - CheckBox
        //type == 2, 3 - SliderBox, ChoiceBox

        var int x_offset = (_Display__get_width()  - 800)/2;
        var int y_offset = (_Display__get_height() - 600)/2;
        var Rect rect;
        Rect_load_XYWH(rect, 80 + x_offset + x_pos * 320, 140 + y_offset + y_pos * 32, 320, 32);

        var Rect left;
        var Rect right;
        Rect_load_LTRB(left, rect.left, rect.top, rect.right - 180, rect.bottom);
        Rect_load_LTRB(right, rect.left + 140, rect.top, rect.right, rect.bottom);

        if type == 3 or type == 2 then
            label_component:set_rect(_toRect(left));
            control:set_rect(_toRect(right));
        else/if type == 1 then  
            control:set_rect(_Rect(_Vector2i(rect.left, rect.top), _Size(32, 32)));
            label_component:set_rect(_Rect(rect.left + 40, rect.top, rect.right, rect.bottom));
        else
            _assert(not "Unhandled control type");
        end

        gui_manager.add(label_component);
        gui_manager.add(control);

        options.push_back(_Option(label_component, control));

        y_pos = y_pos + 1;
    end

    MakeCheckBox(software_cursor_box);   
    MakeCheckBox(fullscreen_box);
    MakeCheckBox(autoscroll_box);
    MakeCheckBox(dragdrop_scroll_box);
    MakeCheckBox(mousegrab_box);
    MakeCheckBox(printfps_box);

    MakeSliderBox(master_volume_box, 25);
    MakeSliderBox(sound_volume_box, 25);
    MakeSliderBox(music_volume_box, 25);

    master_volume_box.set_value(_config_manager.get_master_volume());
    sound_volume_box.set_value(_config_manager.get_sound_volume());
    music_volume_box.set_value(_config_manager.get_music_volume());

    x_pos = 0;
    y_pos = 0;
    AddItem(fullscreen_label, "Fullscreen", fullscreen_box, 1);
    AddItem(mousegrab_label, "Mouse Grab", mousegrab_box, 1);
    y_pos = y_pos + 1;
    AddItem(software_cursor_label, "Software Cursor", software_cursor_box, 1);
    AddItem(autoscroll_label, "Autoscrolling", autoscroll_box, 1);
    AddItem(dragdrop_scroll_label, "Drag&Drop Scrolling", dragdrop_scroll_box, 1);
    y_pos = y_pos + 1;
    AddItem(printfps_label, "Print FPS", printfps_box, 1);

    x_pos = 1;
    y_pos = 0;
    AddItem(resolution_label, "Resolution:", resolution_box, 3);
    AddItem(renderer_label, "Renderer:", renderer_box, 3);
    y_pos = y_pos + 1;
    AddItem(language_label, "Language:", language_box, 3);
    y_pos = y_pos + 1;
    AddItem(master_volume_label, "Master Volume:", master_volume_box, 2);
    AddItem(sound_volume_label, "Sound Volume:", sound_volume_box, 2);
    AddItem(music_volume_label, "Music Volume:", music_volume_box, 2);
  
    mousegrab_box.set_state(_config_manager.get_mouse_grab());
    printfps_box.set_state(_config_manager.get_print_fps());
    fullscreen_box.set_state(_config_manager.get_fullscreen());
    software_cursor_box.set_state(_config_manager.get_software_cursor());
    autoscroll_box.set_state(_config_manager.get_auto_scrolling());
    dragdrop_scroll_box.set_state(_config_manager.get_drag_drop_scrolling());

    function (void) => void close_screen do
        _log_debug("OptionMenu: popping screen");
        _ScreenManager__instance():pop_screen();
        
        // save configuration
        var _Pathname&? cfg_filename = _new_Pathname(_scc2(_System__get_userdir(), "config"), _Pathname__SYSTEM_PATH);
        _log_info("saving configuration: %1%", _deref(cfg_filename));
        _config_manager.get_options().save(_deref(cfg_filename));
        _delete_obj(cfg_filename);
    end

    var Sprite background;
    var Sprite blackboard;
    call/rec background.reload_by_name("core/menu/wood");
    call/rec blackboard.reload_by_name("core/menu/blackboard");

    par do
        //// *.changed ////
        every v in software_cursor_box.changed do
            _config_manager.set_software_cursor(v);
        end
    with
        every v in fullscreen_box.changed do
            _config_manager.set_fullscreen(v);
        end
    with
        every v in autoscroll_box.changed do
            _config_manager.set_auto_scrolling(v);
        end
    with
        every v in dragdrop_scroll_box.changed do
            _config_manager.set_drag_drop_scrolling(v);
        end
    with
        every v in mousegrab_box.changed do
            _config_manager.set_mouse_grab(v);
        end
    with
        every v in printfps_box.changed do
            _config_manager.set_print_fps(v);
        end
    with
        every v in master_volume_box.changed do        
            _config_manager.set_master_volume(v);
        end
    with
        every v in sound_volume_box.changed do
            _config_manager.set_sound_volume(v);
        end
    with
        every v in music_volume_box.changed do
            _config_manager.set_music_volume(v);
        end
    with
        every v in resolution_box.changed do
            var Size size_;
            var int refresh_rate;
            if _sscanf(v, "%dx%d@%d", &size_.width, &size_.height, &refresh_rate) != 3 then
                _log_error("failed to parse: %1%", v);
            else
                #ifdef OLD_SDL1
                    //original FIXME: ignoring refresh rate here
                #endif

                _config_manager.set_fullscreen_resolution(_toSize(size_)); 
            end
        end
    with
        every v in renderer_box.changed do
            _config_manager.set_renderer(_framebuffer_type_from_string(_PTR2REF(v)));
        end
    with
        every v in language_box.changed do
            if option_menu.m_language_map.count(_PTR2REF(v)) then
                option_menu.m_language = _PTR2REF(option_menu.m_language_map.at(_PTR2REF(v)));
                _config_manager.set_language(_PTR2REF(option_menu.m_language));
            else  
                _log_error("unknown language: %1%", v);        
            end
        end
        ///////////////////
    with
        every package in SCREEN_DRAW_BACKGROUND do
            if package:screen != &option_menu then
                continue;
            end
            
            // Paint the background wood panel
            var int y = 0;
            loop do
                if y >= package:gc.get_height() then break; end
                var int x = 0;
                loop do
                    if x >= package:gc.get_width() then break; end
                    package:gc.draw(_deref(background.get()), _Vector2i(x, y));
                    x = x + background.get_width();
                end
                y = y + background.get_height();
            end

            package:gc.draw(_deref(blackboard.get()), _Vector2i(package:gc.get_width()/2, package:gc.get_height()/2));
    
            package:gc.print_center(_Fonts__chalk_large, _Vector2i(package:gc.get_width()/2, package:gc.get_height()/2 - 240), __("Option Menu"));

            package:gc.print_center(_Fonts__chalk_normal, _Vector2i(package:gc.get_width()/2 + 245 + 30, package:gc.get_height()/2 + 150 - 20), __("Close"));

            package:gc.print_left(_Fonts__chalk_normal, _Vector2i(package:gc.get_width()/2 - 320, package:gc.get_height()/2 + 200),
                        __("Some options require a restart of the game to take effect."));

            emit gui_manager.draw => _addr(package:gc);
        end
    with
        every package in SCREEN_UPDATE do
            if package:screen == &option_menu then
                emit gui_manager.update => package:delta;
            end
        end
    with
        every screen in ON_ESCAPE_PRESS do
            if screen == &option_menu then
                close_screen();
            end
        end
    with
        every package in SCREEN_RESIZE do
            if package:screen != &option_menu then
                continue;
            end

            call/rec ok_button.set_pos(option_menu.size.width/2 + 245, option_menu.size.height/2 + 150);

            //original FIXME: this drifts due to rounding errors
            var int x_diff = (option_menu.size.width  - option_menu.old_size.width) / 2;
            var int y_diff = (option_menu.size.height - option_menu.old_size.height) / 2;

            var Rect rect;
            loop i in options.size() do
                if options.at(i).label then                    
                    _readRect(rect, options.at(i).label:get_rect());                    
                    options.at(i).label:set_rect(_Rect(_Vector2i(rect.left + x_diff, rect.top + y_diff), _Size(rect.right - rect.left, rect.bottom - rect.top)));
                end
                
                _readRect(rect, options.at(i).control:get_rect());
                options.at(i).control:set_rect(_Rect(_Vector2i(rect.left + x_diff, rect.top + y_diff), _Size(rect.right - rect.left, rect.bottom - rect.top)));
            end
        end
    end
end

#endif
