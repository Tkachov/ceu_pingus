#ifndef WORLD_CEU
#define WORLD_CEU

#include "../c.ceu"
#include "../sdl.ceu"
#include "iworld.ceu"
#include "pingu.ceu"
#include "pingu_holder.ceu"
#include "ground_map.ceu"
#include "particles/pingu_particles_holder.ceu"
#include "particles/rain_particles_holder.ceu"
#include "particles/smoke_particles_holder.ceu"
#include "particles/snow_particles_holder.ceu"
#include "worldobjs/conveyor_belt.ceu"
#include "worldobjs/entrance.ceu"
#include "worldobjs/exit.ceu"
#include "worldobjs/fake_exit.ceu"
#include "worldobjs/groundpiece.ceu"
#include "worldobjs/guillotine.ceu"
#include "worldobjs/hammer.ceu"
#include "worldobjs/hotspot.ceu"
#include "worldobjs/ice_block.ceu"
#include "worldobjs/laser_exit.ceu"
#include "worldobjs/liquid.ceu"
#include "worldobjs/rain_generator.ceu"
#include "worldobjs/smasher.ceu"
#include "worldobjs/snow_generator.ceu"
#include "worldobjs/solid_color_background.ceu"
#include "worldobjs/spike.ceu"
#include "worldobjs/starfield_background.ceu"
#include "worldobjs/surface_background.ceu"
#include "worldobjs/switch_door_door.ceu"
#include "worldobjs/switch_door_switch.ceu"
#include "worldobjs/teleporter.ceu"
#include "worldobjs/teleporter_target.ceu"

input _WOCPackage* NEW_CONVEYOR_BELT;
input _WOCPackage* NEW_ENTRANCE;
input _WOCPackage* NEW_EXIT;
input _WOCPackage* NEW_FAKE_EXIT;
input _WOCPackage* NEW_GROUNDPIECE;
input _WOCPackage* NEW_GUILLOTINE;
input _WOCPackage* NEW_HAMMER;
input _WOCPackage* NEW_HOTSPOT;
input _WOCPackage* NEW_ICE_BLOCK;
input _WOCPackage* NEW_LASER_EXIT;
input _WOCPackage* NEW_LIQUID;
input _WOCPackage* NEW_RAIN_GENERATOR;
input _WOCPackage* NEW_SMASHER;
input _WOCPackage* NEW_SNOW_GENERATOR;
input _WOCPackage* NEW_SOLID_COLOR_BACKGROUND;
input _WOCPackage* NEW_SPIKE;
input _WOCPackage* NEW_STARFIELD_BACKGROUND;
input _WOCPackage* NEW_SURFACE_BACKGROUND;
input _WOCPackage* NEW_SWITCH_DOOR_DOOR;
input _WOCPackage* NEW_SWITCH_DOOR_SWITCH;
input _WOCPackage* NEW_TELEPORTER;
input _WOCPackage* NEW_TELEPORTER_TARGET;

input _GetStartPosPackage* WORLD_GET_START_POS;
input _PrefabPackage* PREFAB_POS;

native/pre do
    ##define reader_read_Groundtype(r, s, v) r->read_enum(s, v, &Groundtype::string_to_type);

    ##include "pingus/resource.hpp"
    ##define Resource__load_surface Resource::load_surface

    ##define opeq(x, y) *x = y
    ##define new_Surface() new Surface()

    ##include "util/container.h"
    ##define new_Container() new Container()

    ##define read_position(r, p) Vector3f __v; r->read_vector("position", __v); p.x = __v.x; p.y = __v.y; p.z = __v.z;
    ##define read_position2(r, p, res) Vector3f __v; res = r->read_vector("position", __v); p.x = __v.x; p.y = __v.y; p.z = __v.z;
end

native @pure _reader_read_Groundtype(), _Resource__load_surface(), _opeq(), _new_Surface(), _new_Container(),
             _read_position(), _read_position2();

input _World* WORLD_STARTUP;
input _World* WORLD_DELETE;

input IWorldObj* WORLD_STARTUP_OBJECT;
input IWorldObj* WORLD_DRAW_OBJECT;
input IWorldObj* WORLD_DRAW_SMALLMAP_OBJECT;
input IWorldObj* WORLD_UPDATE_OBJECT;
input IWorldObj* WORLD_DELETE_OBJECT;

class World with
    interface IWorld;
    pool Entrance[] entrances;
    pool ITeleporterTarget[] teleporter_targets;
    pool ISwitchDoorDoor[] switch_door_doors;
do
    var _World&? me = _PTR2REF((_World*)world);
    var _Container&? objs = _PTR2REF(_new_Container());
    finalize with _delete_obj(objs); end

    function @rec (IWorldObj* o) => void add_object do
        objs.add((void*)o, o:get_z_pos());
        objs.sort();
    end

    function (IWorldObj* o) => void remove_object do
        objs.remove((void*)o);
    end

    var PinguHolder holder with
        this.holder = me.pingus;
    end;

    var PinguParticlesHolder pingu_particles_holder with
        this.sprite = _PTR2REF(_new_SpriteA("particles/pingu_explo"));
        this.particles = _PTR2REF(_new_Vector_PinguParticle());
    end;

    var RainParticlesHolder rain_particles_holder with
        this.rain1_surf = _PTR2REF(_new_SpriteA("particles/rain1"));
        this.rain2_surf = _PTR2REF(_new_SpriteA("particles/rain2"));
        this.rain_splash = _PTR2REF(_new_SpriteA("particles/rain_splash"));
        this.particles = _PTR2REF(_new_Vector_RainParticle());
    end;

    var SmokeParticlesHolder smoke_particles_holder with
        this.surf1 = _PTR2REF(_new_SpriteA("particles/smoke"));
        this.surf2 = _PTR2REF(_new_SpriteA("particles/smoke2"));
        this.particles = _PTR2REF(_new_Vector_SmokeParticle());
    end;

    var SnowParticlesHolder snow_particles_holder with
        this.snow1 = _PTR2REF(_new_SpriteA("particles/snow1"));
        this.snow2 = _PTR2REF(_new_SpriteA("particles/snow2"));
        this.snow3 = _PTR2REF(_new_SpriteA("particles/snow3"));
        this.snow4 = _PTR2REF(_new_SpriteA("particles/snow4"));
        this.snow5 = _PTR2REF(_new_SpriteA("particles/snow5"));
        this.ground = _PTR2REF(_new_CollisionMask("particles/ground_snow"));
        this.particles = _PTR2REF(_new_Vector_SnowParticle());
    end;

    var GroundMap gfx_map with
        this.gmap = _PTR2REF(_new_GroundMap(me.mwidth, me.mheight));

        me.gfx_map = _PTR2REF(this.gmap);
        me.colmap = _PTR2REF(this.gmap.colmap.get());
    end;

    function (void) => IPinguHolder* get_pingus do
        return holder;
    end

    function (void) => IPinguParticlesHolder* get_pingu_particles_holder do
        return pingu_particles_holder;
    end

    function (void) => IRainParticlesHolder* get_rain_particles_holder do
        return rain_particles_holder;
    end

    function (void) => ISmokeParticlesHolder* get_smoke_particles_holder do
        return smoke_particles_holder;
    end

    function (void) => ISnowParticlesHolder* get_snow_particles_holder do
        return snow_particles_holder;
    end

    function (_const_char_string id) => ITeleporterTarget* get_teleporter_target do
        loop target in teleporter_targets do
            if target:id == id then
                return target;
            end
        end

        return null;
    end

    function (_const_char_string id) => ISwitchDoorDoor* get_switch_door_door do
        loop door in switch_door_doors do
            if door:id == id then
                return door;
            end
        end

        return null;
    end

    function @rec (_CollisionMask* mask, int x, int y, int type) => void put do
        world:gfx_map:put(mask:get_surface(), x, y);
        world:colmap:put(_deref(mask), x, y, _to_GT(type));
    end

    function @rec (_CollisionMask* mask, int x, int y) => void remove do
        world:gfx_map:remove(mask:get_surface(), x, y);
        world:colmap:remove(_deref(mask), x, y);
    end

    function @rec (_const_char_string str, int x, int y, int t) => void put_str do
        var _CollisionMask* mask = _new_CollisionMask(str);
        call/rec put(mask, x, y, t);
        _delete_obj(mask);
    end

    function @rec (_const_char_string str, int x, int y) => void remove_str do
        var _CollisionMask* mask = _new_CollisionMask(str);
        call/rec remove(mask, x, y);
        _delete_obj(mask);
    end

    var bool draw_iterating = false;
    var _SceneContext* gc = null;

    var bool draw_smallmap_iterating = false;
    var _SmallMap* smallmap = null;

    par/or do
        every w in WORLD_STARTUP do
            if w != &me then
                continue;
            end

            objs.iterate(_CEU_IN_WORLD_STARTUP_OBJECT);
        end
    with
        every object in WORLD_STARTUP_OBJECT do
            emit object:e_STARTUP;
        end
    with
        every wgc in e_DRAW do
            wgc:light().fill_screen(_Color(me.ambient_light) finalize with end);

            draw_iterating = true;
            gc = _PTR2REF(wgc);
            objs.iterate(_CEU_IN_WORLD_DRAW_OBJECT);
            draw_iterating = false;
            gc = null;
        end
    with
        every object in WORLD_DRAW_OBJECT do
            if draw_iterating and gc != null then
                emit object:e_DRAW => gc;
            end
        end
    with
        every sm in e_DRAW_SMALLMAP do
            draw_smallmap_iterating = true;
            smallmap = _PTR2REF(sm);
            objs.iterate(_CEU_IN_WORLD_DRAW_SMALLMAP_OBJECT);
            draw_smallmap_iterating = false;
            smallmap = null;
        end
    with
        every object in WORLD_DRAW_SMALLMAP_OBJECT do
            if draw_smallmap_iterating and smallmap != null then
                emit object:e_DRAW_SMALLMAP => smallmap;
            end
        end
    with
        every e_UPDATE do
            me.game_time = me.game_time + 1;

            if me.do_armageddon and me.game_time%4 == 0 then
                loop pingu in holder.all_pingus do
                    if pingu:get_current_action() == _A_BOMBER or pingu:get_current_action() == _A_DROWN or
                       pingu:get_current_action() == _A_SPLASHED or pingu:get_current_action() == _A_LASERKILL or
                       pingu:get_current_action() == _A_DEAD or pingu:get_current_action() == _A_EXITED then
                        continue;
                    end

                    emit pingu:e_CHANGE_ACTION => _A_BOMBER;
                    break;
                end
            end

            objs.iterate(_CEU_IN_WORLD_UPDATE_OBJECT);
        end
    with
        every object in WORLD_UPDATE_OBJECT do
            emit object:e_UPDATE;
        end
    with
        every e_ARMAGEDDON do
            _Sound_PingusSound_play_sound("goodidea");
            me.do_armageddon = true;
            me.armageddon_count = 0;
        end
    with
        every p4ckage in PREFAB_POS do
            var IWorldObj* wobject = (IWorldObj*)p4ckage:object;
            //wobject:set_pos(wobject:get_pos() + p4ckage:pos);
            wobject:pos.x = wobject:pos.x + p4ckage:pos.x;
            wobject:pos.y = wobject:pos.y + p4ckage:pos.y;
            wobject:pos.z = wobject:pos.z + p4ckage:pos.z;
        end
    with
        every packag3 in WORLD_GET_START_POS do
            if packag3:world != &me then
                continue;
            end

            var int num_entrances = 0;

            loop entrance in entrances do
                if entrance:owner_id == packag3:player_id then
                    packag3:pos.x = packag3:pos.x + entrance:get_pos().x;
                    packag3:pos.y = packag3:pos.y + entrance:get_pos().y;
                    num_entrances = num_entrances + 1;
                end
            end

            if num_entrances>0 then
                packag3:pos.x = packag3:pos.x/num_entrances;
                packag3:pos.y = packag3:pos.y/num_entrances;
                packag3:pos.y = packag3:pos.y+100;
            end
        end
    with
        every package in NEW_CONVEYOR_BELT do
            spawn ConveyorBelt with
                package:result = &this;

                this.left_sur = _PTR2REF(_new_SpriteA("worldobjs/conveyorbelt_left"));
                this.right_sur = _PTR2REF(_new_SpriteA("worldobjs/conveyorbelt_right"));
                this.middle_sur = _PTR2REF(_new_SpriteA("worldobjs/conveyorbelt_middle"));

                _read_position(package:reader, this.pos);
                if not package:reader:read_int("repeat", _PTR2REF(this.width)) then
                    _log_warn("old 'width' tag used");
                    package:reader:read_int("width", _PTR2REF(this.width));
                end
                package:reader:read_float("speed", _PTR2REF(this.speed));
            end;
        end
    with
        every package in NEW_ENTRANCE do
            spawn Entrance in entrances with
                package:result = &this;

                this.direction = ED_MISC;
                this.release_rate = 150;
                this.owner_id = 0;
                this.smallmap_symbol = _PTR2REF(_new_SpriteA("core/misc/smallmap_entrance"));
                this.surface = _PTR2REF(_new_Sprite());

                package:reader:read_int("owner-id", _PTR2REF(this.owner_id));
                _read_position(package:reader, this.pos);
                package:reader:read_int("release-rate",  _PTR2REF(this.release_rate));

                // Set default owner ID to 0
                if this.owner_id<0 or this.owner_id>3 then
                    this.owner_id = 0;
                end

                var _String&? direction_str = _PTR2REF(_new_String());
                _string_read_string(direction_str, package:reader, "direction");

                if direction_str == "left" then
                    this.direction = ED_LEFT;
                else/if direction_str == "right" then
                    this.direction = ED_RIGHT;
                else/if direction_str == "misc" then
                    this.direction = ED_MISC;
                else
                    _log_error("unknown direction: '%1%'", direction_str);
                    this.direction = ED_MISC;
                end

                _delete_obj(direction_str);
            end;
        end;
    with
        every package in NEW_EXIT do
            spawn Exit with
                package:result = &this;

                this.desc = _PTR2REF(_new_ResDescriptor());
                this.smallmap_symbol = _PTR2REF(_new_SpriteA("core/misc/smallmap_exit"));

                _read_position(package:reader, this.pos);
                this.construct(package:reader); //can't put reference initiation there =(

                this.flag = _PTR2REF(_new_SpriteA(_scc2("core/misc/flag", _StringUtil__to_string(this.owner_id))));
                this.sprite = _PTR2REF(_new_SpriteA(_deref(this.desc)));
            end;
        end
    with
        every package in NEW_FAKE_EXIT do
            spawn FakeExit with
                package:result = &this;

                this.sprite = _PTR2REF(_new_SpriteA("traps/fake_exit"));
                this.smallmap_symbol = _PTR2REF(_new_SpriteA("core/misc/smallmap_exit"));
                this.smashing = false;

                _read_position(package:reader, this.pos);
                this.pos.x = this.pos.x - (float)this.sprite.get_width()/2;
                this.pos.y = this.pos.y - (float)this.sprite.get_height();
            end;
        end
    with
        every package in NEW_GROUNDPIECE do
            spawn Groundpiece with
                package:result = &this;

                _read_position(package:reader, this.pos);
                this.desc = _PTR2REF(_new_ResDescriptor());
                package:reader:read_desc("surface", _deref(this.desc));
                this.gptype = _GT_GROUND;
                _reader_read_Groundtype(package:reader, "type", _PTR2REF(this.gptype));
            end;
        end
    with
        every package in NEW_GUILLOTINE do
            spawn Guillotine with
                package:result = &this;

                this.sprite_kill_right = _PTR2REF(_new_SpriteA("traps/guillotinekill/right"));
                this.sprite_kill_left = _PTR2REF(_new_SpriteA("traps/guillotinekill/left"));
                this.sprite_idle = _PTR2REF(_new_SpriteA("traps/guillotineidle"));
                this.direction = _new_Direction();
                this.killing = false;

                _read_position(package:reader, this.pos);
                this.sprite_kill_right.set_play_loop(false);
                this.sprite_kill_left.set_play_loop(false);
                this.sprite_idle.set_play_loop(true);
            end;
        end
    with
        every package in NEW_HAMMER do
            spawn Hammer with
                package:result = &this;

                this.sprite = _PTR2REF(_new_SpriteA("traps/hammer"));
                _read_position(package:reader, this.pos);
            end;
        end
    with
        every package in NEW_HOTSPOT do
            spawn Hotspot with
                package:result = &this;

                var _ResDescriptor&? desc = _PTR2REF(_new_ResDescriptor());

                _read_position(package:reader, this.pos);
                package:reader:read_desc  ("surface",  _deref(desc));
                package:reader:read_float ("parallax", _PTR2REF(this.para));

                this.sprite = _PTR2REF(_new_SpriteA(_deref(desc)));

                _delete_obj(desc);
            end;
        end
    with
        every package in NEW_ICE_BLOCK do
            spawn IceBlock with
                package:result = &this;

                this.block_sur = _PTR2REF(_new_SpriteA("worldobjs/iceblock"));
                this.block_sur_cmap = _PTR2REF(_new_CollisionMask("worldobjs/iceblock_cmap"));

                _read_position(package:reader, this.pos);
                if not package:reader:read_int("repeat", _PTR2REF(this.width)) then
                    _log_warn("old 'width' tag used");
                    package:reader:read_int("width", _PTR2REF(this.width));
                end
            end;
        end
    with
        every package in NEW_LASER_EXIT do
            spawn LaserExit with
                package:result = &this;

                this.surface = _PTR2REF(_new_SpriteA("traps/laser_exit"));
                _read_position(package:reader, this.pos);
            end;
        end
    with
        every package in NEW_LIQUID do
            spawn Liquid with
                package:result = &this;

                this.width = 0;

                var _ResDescriptor&? desc = _PTR2REF(_new_ResDescriptor());

                _read_position(package:reader, this.pos);
                package:reader:read_desc  ("surface",  _deref(desc));
                package:reader:read_int   ("repeat",   _PTR2REF(this.width));

                this.sprite = _PTR2REF(_new_SpriteA(_deref(desc)));
            end;
        end
    with
        every package in NEW_RAIN_GENERATOR do
            spawn RainGenerator with
                package:result = &this;
            end;
        end
    with
        every package in NEW_SMASHER do
            spawn Smasher with
                package:result = &this;

                this.sprite = _PTR2REF(_new_SpriteA("traps/smasher"));
                _read_position(package:reader, this.pos);
                _assert(this.sprite.get_frame_count() == 6);
            end;
        end
    with
        every package in NEW_SNOW_GENERATOR do
            spawn SnowGenerator with
                package:result = &this;

                this.intensity = 1;
                package:reader:read_float("intensity", _PTR2REF(this.intensity));
            end;
        end
    with
        every package in NEW_SOLID_COLOR_BACKGROUND do
            spawn SolidColorBackground with
                package:result = &this;

                this.color = _PTR2REF(_new_Color());
                if not package:reader:read_colori("colori", _deref(this.color)) then
                    package:reader:read_colorf("color", _deref(this.color));
                end
            end;
        end
    with
        every package in NEW_SPIKE do
            spawn Spike with
                package:result = &this;

                this.surface = _PTR2REF(_new_SpriteA("traps/spike"));
                _read_position(package:reader, this.pos);
            end;
        end
    with
        every package in NEW_STARFIELD_BACKGROUND do
            spawn StarfieldBackground with
                package:result = &this;

                this.small_stars_count = 100;
                this.middle_stars_count = 50;
                this.large_stars_count = 25;

                package:reader:read_int("small-stars", _PTR2REF(this.small_stars_count));
                package:reader:read_int("middle-stars", _PTR2REF(this.middle_stars_count));
                package:reader:read_int("large-stars", _PTR2REF(this.large_stars_count));
            end;
        end
    with
        every package in NEW_SURFACE_BACKGROUND do
            spawn SurfaceBackground with
                package:result = &this;

                this.para_x = 0.5;
                this.para_y = 0.5;
                this.scroll_x = 0;
                this.scroll_y = 0;
                this.color = _PTR2REF(_new_Color(0, 0, 0, 0));
                this.stretch_x = false;
                this.stretch_y = false;
                this.keep_aspect = false;
                this.scroll_ox = 0;
                this.scroll_oy = 0;

                var bool res;
                _read_position2(package:reader, this.pos, res);
                if not res then
                    this.pos.x = 0;
                    this.pos.y = 0;
                    this.pos.z = -150;
                end

                var _ResDescriptor&? desc = _PTR2REF(_new_ResDescriptor());
                package:reader:read_desc("surface",  _deref(desc));

                if not package:reader:read_colori("colori", _deref(this.color)) then
                    package:reader:read_colorf("color", _deref(this.color));
                end

                package:reader:read_float("para-x", _PTR2REF(this.para_x));
                package:reader:read_float("para-y", _PTR2REF(this.para_y));

                package:reader:read_float("scroll-x", _PTR2REF(this.scroll_x));
                package:reader:read_float("scroll-y", _PTR2REF(this.scroll_y));

                package:reader:read_bool("stretch-x", _PTR2REF(this.stretch_x));
                package:reader:read_bool("stretch-y", _PTR2REF(this.stretch_y));

                package:reader:read_bool("keep-aspect", _PTR2REF(this.keep_aspect));

                if not this.stretch_x and not this.stretch_y and this.color.a == 0 then
                    //orignal FIXME: would be nice to allow surface manipulation with
                    // animated sprites, but it's not that easy to do
                    this.sprite = _PTR2REF(_new_SpriteA(_deref(desc)));
                else
                    var _Surface&? surface = _new_Surface();
                    _opeq(surface, _Resource__load_surface(_deref(desc)));

                    if this.color.a != 0 and surface.is_indexed() then
                        if surface.has_colorkey() then
                            _opeq(surface, surface.convert_to_rgba());
                        else
                            _opeq(surface, surface.convert_to_rgb());
                        end
                    end

                    surface.fill(_deref(this.color));

                    // Scaling Code
                    if this.stretch_x and this.stretch_y then
                        _opeq(surface, surface.scale(global:world:world:get_width(), global:world:world:get_height()));
                    else/if this.stretch_x and not this.stretch_y then
                        if this.keep_aspect then
                            var float aspect = (float)surface.get_height() / (float)surface.get_width();
                            _opeq(surface, surface.scale(global:world:world:get_width(), (int)((float)global:world:world:get_width()*aspect)));
                        else
                            _opeq(surface, surface.scale(global:world:world:get_width(), surface.get_height()));
                        end
                    else/if not this.stretch_x and this.stretch_y then
                        if this.keep_aspect then
                            var float aspect = (float)surface.get_width() / (float)surface.get_height();
                            _opeq(surface, surface.scale((int)((float)global:world:world:get_height()*aspect), global:world:world:get_height()));
                        else
                            _opeq(surface, surface.scale(surface.get_width(), global:world:world:get_height()));
                        end
                    end

                    this.sprite = _PTR2REF(_new_SpriteA(_deref(surface)));
                end

                _delete_obj(desc);
            end;
        end
    with
        every package in NEW_SWITCH_DOOR_DOOR do
            spawn SwitchDoorDoor in switch_door_doors with
                package:result = &this;

                this.id = _PTR2REF(_new_String());
                _string_read_string(this.id, package:reader, "id");

                this.door_box = _PTR2REF(_new_SpriteA("worldobjs/switchdoor_box"));
                this.door_box_cmap = _PTR2REF(_new_CollisionMask("worldobjs/switchdoor_box"));
                this.door_tile = _PTR2REF(_new_SpriteA("worldobjs/switchdoor_tile"));
                this.door_tile_cmap = _PTR2REF(_new_CollisionMask("worldobjs/switchdoor_tile_cmap"));
                this.is_opening = false;

                _read_position(package:reader, this.pos);

                package:reader:read_int("height", _PTR2REF(this.door_height));
                this.current_door_height = this.door_height;
            end;
        end
    with
        every package in NEW_SWITCH_DOOR_SWITCH do
            spawn SwitchDoorSwitch with
                package:result = &this;

                this.switch_sur = _PTR2REF(_new_SpriteA("worldobjs/switchdoor_switch"));
                this.is_triggered = false;
                this.m_target = _PTR2REF(_new_String());
                _string_read_string(this.m_target, package:reader, "target-id");
                _read_position(package:reader, this.pos);
            end;
        end
    with
        every package in NEW_TELEPORTER do
            spawn Teleporter with
                package:result = &this;

                this.sprite = _PTR2REF(_new_SpriteA("worldobjs/teleporter"));
                _read_position(package:reader, this.pos);
                this.target_id = _PTR2REF(_new_String());
                _string_read_string(this.target_id, package:reader, "target-id");
            end;
        end
    with
        every package in NEW_TELEPORTER_TARGET do
            spawn TeleporterTarget in teleporter_targets with
                package:result = &this;

                this.id = _PTR2REF(_new_String());
                _string_read_string(this.id, package:reader, "id");
                this.sprite = _PTR2REF(_new_SpriteA("worldobjs/teleportertarget"));
                _read_position(package:reader, this.pos);
            end;
        end
    with
        every w in WORLD_DELETE do
            if w == &me then
                objs.iterate(_CEU_IN_WORLD_DELETE_OBJECT);
                break;
            end
        end
    with
        every object in WORLD_DELETE_OBJECT do
            emit object:e_DELETE;
        end
    end
end

#endif
