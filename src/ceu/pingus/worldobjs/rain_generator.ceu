#ifndef RAIN_GENERATOR_CEU
#define RAIN_GENERATOR_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/sound/sound.ceu"
#include "../../math/vector3f.ceu"
#include "../../math/math.ceu"
#include "../iworld.ceu"
#include "../worldobj.ceu"

class RainGenerator with
    interface IWorldObj;
    var bool do_thunder;
    var float thunder_count;
    var float waiter_count;

do
    call/rec global:world:add_object(&this);

    function (_FileReader* reader) => void construct do end;

    function (void) => float get_z_pos do return 1000; end
    function (Vector3f p) => void set_pos do end
    function @rec (void) => Vector3f get_pos do return pos; end

    do_thunder = false;
    thunder_count = 0;
    waiter_count = 0;

    par/or do
        every gc in global:draw do
            if do_thunder then
                if thunder_count < 0 then
                    do_thunder = false;
                    thunder_count = 0;
                    waiter_count = 1;
                end

                //gc.color().fill_screen(Color(255, 255, 255, static_cast<uint8_t>(thunder_count*255)));
                gc:color().fill_screen(_Color(255, 255, 255, (u8)(thunder_count*255)));
            end
        end
    with
        every e_UPDATE do
            if waiter_count<0 and _rand()%150 == 0 then
                _log_info("Doing thunder");
                do_thunder = true;
                thunder_count = 1;
                waiter_count = 1;
                _Sound_PingusSound_play_sound("thunder");
            end

            if do_thunder then
                thunder_count = thunder_count - 10 * 0.025;
            end

            waiter_count = waiter_count - 20.0 * 0.025;

            loop i in 16 do
                call/rec global:world:get_rain_particles_holder():add_particle(_rand()%(global:world:get_width()*2), -32);
            end
        end
    with
        await e_DELETE;
    end
end

#endif
