#ifndef EXIT_CEU
#define EXIT_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../iworld.ceu"
#include "../worldobj.ceu"
#include "../ipingu_holder.ceu"
#include "../pingu_holder_listener.ceu"

native/pre do
    ##include "pingus/components/smallmap.hpp"
    ##include "pingus/res_descriptor.hpp"
    ##define new_ResDescriptor() new ResDescriptor()
    ##define delete_ResDescriptor(x) delete x;
    ##define colmap_remove(cm, desc, x, y) { CollisionMask __mask(*desc); cm->remove(__mask, x, y); }
end

native @pure _new_ResDescriptor(), _delete_ResDescriptor(), _colmap_remove(), _deref();

class Exit with
    interface IWorldObj;
    var _ResDescriptor&? desc;
    var int owner_id;

    var _Sprite&? sprite;
    var _Sprite&? flag;
    var _Sprite&? smallmap_symbol;
do
    var _WorldObj&? exit = _PTR2REF(me);
    //finalize with _delete_WorldObj(me); end
    finalize with _delete_ResDescriptor(desc); end
    finalize with _delete_Sprite(sprite); end;
    finalize with _delete_Sprite(flag); end;
    finalize with _delete_Sprite(smallmap_symbol); end;

    par/or do
        every package in WORLDOBJ_DRAW do
            if package:worldobj != &exit then
                continue;
            end

            _gc_color_draw2(package:gc, &sprite, exit.pos);
            _gc_color_draw2(package:gc, &flag, exit.pos + _Vector3f(40, 0));
        end
    with
        every wo in WORLDOBJ_STARTUP do
            if wo != &exit then
                continue;
            end

            _colmap_remove(exit.world:get_colmap(), desc, (int)exit.pos.x - sprite.get_width()/2, (int)exit.pos.y - sprite.get_height());
        end
    with
        every package in WORLDOBJ_DRAW_SMALLMAP do
            if package:worldobj != &exit then
                continue;
            end

            package:smallmap:draw_sprite(_deref(smallmap_symbol), _PTR2REF(exit.pos));
        end
    with
        every w in WORLD_UPDATE do
            if w != exit.world then
                continue;
            end

            sprite.update();

            loop pingu in global:world:get_pingus():all_pingus do
                // Make sure this particular exit is allowed for this pingu
                if pingu:get_owner() != owner_id then
                    continue;
                end

                // Now, make sure the pingu is within range
                if pingu:get_x() <= exit.pos.x-1 or pingu:get_x() >= exit.pos.x+1 or
                   pingu:get_y() <= exit.pos.y-5 or pingu:get_y() >= exit.pos.y+5 then
                    continue;
                end

                // Now, make sure the pingu isn't already exiting, gone, or dead [or is dying because of apocalypse/bomber action]
                if pingu:get_current_action() != _A_EXITER and pingu:get_current_action() != _A_BOMBER and
                   pingu:get_current_action() != _A_DEAD and pingu:get_current_action() != _A_EXITED then
                    // Pingu actually exits
                    emit pingu:e_CHANGE_ACTION => _A_EXITER;
                end
            end
        end
    with
        var _WorldObj* e = await DELETE_WORLDOBJ until (e == &exit);
    end
end

#endif
