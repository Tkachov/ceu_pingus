#ifndef STARFIELD_BACKGROUND_STARS_CEU
#define STARFIELD_BACKGROUND_STARS_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../iworld.ceu"
#include "../worldobj.ceu"
#include "../ipingu_holder.ceu"
#include "../global_interface.ceu"

native/pre do
    ##define globals__tile_size globals::tile_size
end

class StarfieldBackgroundStars with
    var _Sprite&? sprite;

    var float x_pos;
    var float y_pos;

    var float x_add;
    var float y_add;
do
    finalize with _delete_obj(sprite); end

    x_pos = (float)(_rand()%global:world:get_width());
    y_pos = (float)(_rand()%global:world:get_height());

    x_add = (float)(_rand()%5)+1.0;
    y_add = 0.0;

    par do
        every gc in global:draw do
            _gc_color_draw2(gc, &sprite, _Vector3f(x_pos, y_pos, -10)); //original FIXME: make z editable via xml
        end
    with
        every global:world:update do
            x_pos = x_pos + x_add;
            y_pos = y_pos + y_add;

            if x_pos > global:world:get_width() then
                x_pos = (float)-_globals__tile_size;
                y_pos = (float)(_rand()%global:world:get_height());
            end
        end
    end
end

#endif
