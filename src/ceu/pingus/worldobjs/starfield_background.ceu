#ifndef STARFIELD_BACKGROUND_CEU
#define STARFIELD_BACKGROUND_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../iworld.ceu"
#include "../worldobj.ceu"
#include "../ipingu_holder.ceu"
#include "../pingu_holder_listener.ceu"
#include "starfield_background_stars.ceu"

class StarfieldBackground with
    interface IWorldObj;

    var int small_stars_count;
    var int middle_stars_count;
    var int large_stars_count;
do
    call/rec global:world:add_object(&this);

    function (_FileReader* reader) => void construct do end;

    //original FIXME: Make z_pos handling editable via xml
    function (void) => float get_z_pos do return -10; end
    function (Vector3f p) => void set_pos do end
    function (void) => Vector3f get_pos do return pos; end

    pool StarfieldBackgroundStars[] stars;

    loop i in small_stars_count do
        spawn StarfieldBackgroundStars in stars with
            this.sprite = _PTR2REF(_new_SpriteA("game/stars/small_star"));
        end;
    end

    loop i in middle_stars_count do
        spawn StarfieldBackgroundStars in stars with
            this.sprite = _PTR2REF(_new_SpriteA("game/stars/middle_star"));
        end;
    end

    loop i in large_stars_count do
        spawn StarfieldBackgroundStars in stars with
            this.sprite = _PTR2REF(_new_SpriteA("game/stars/large_star"));
        end;
    end

    par/or do
        every gc in e_DRAW do
            loop star in stars do
                emit star:e_DRAW => gc;
            end
        end
    with
        every e_UPDATE do
            loop star in stars do
                emit star:e_UPDATE;
            end
        end
    with
        await e_DELETE;
    end
end

#endif
