#ifndef SPIKE_CEU
#define SPIKE_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../iworld.ceu"
#include "../worldobj.ceu"
#include "../ipingu_holder.ceu"
#include "../pingu_holder_listener.ceu"

class Spike with
    interface IWorldObj;
    var _Sprite&? surface;
do
    call/rec global:world:add_object(&this);

    function (_FileReader* reader) => void construct do end;

    function (void) => float get_z_pos do
        return pos.z;
    end

    function (Vector3f p) => void set_pos do
        pos = p;
    end

    function (void) => Vector3f get_pos do
        return pos;
    end

    finalize with
        _delete_obj(surface);
    end;

    var bool killing = false;

    par/or do
        every gc in e_DRAW do
            if killing then
                _gc_color_draw2(gc, &surface, _toVector3f(pos));
            end
        end
    with
        every e_UPDATE do
            if killing then
                surface.update();
            end

            loop pingu in global:world:get_pingus():all_pingus do
                if not killing then
                    if pingu:get_x() > pos.x+16-5 and pingu:get_x() < pos.x+16+5 and
                       pingu:get_y() > pos.y and pingu:get_y() < pos.y+32 then
                      surface.restart();
                      killing = true;
                    end
                else
                    if surface.get_current_frame() == 3 and
                       pingu:get_x() > pos.x+16-12 and pingu:get_x() < pos.x+16+12 and
                       pingu:get_y() > pos.y and pingu:get_y() < pos.y+32 then
                      emit pingu:e_CHANGE_ACTION => _A_DEAD;
                    end
                end
            end

            if surface.get_current_frame() == surface.get_frame_count()-1 then
                killing = false;
            end
        end
    with
        await e_DELETE;
    end
end

#endif
