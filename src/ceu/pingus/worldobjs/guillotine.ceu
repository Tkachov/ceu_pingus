#ifndef GUILLOTINE_CEU
#define GUILLOTINE_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../iworld.ceu"
#include "../worldobj.ceu"
#include "../ipingu_holder.ceu"
#include "../pingu_holder_listener.ceu"

class Guillotine with
    interface IWorldObj;
    var _Sprite&? sprite_kill_right;
    var _Sprite&? sprite_kill_left;
    var _Sprite&? sprite_idle;

    var _Direction&? direction;
    var bool killing;
do
    call/rec global:world:add_object(&this);

    function (_FileReader* reader) => void construct do end;

    function (void) => float get_z_pos do
        return pos.z;
    end

    function (Vector3f p) => void set_pos do
        pos = p;
    end

    function (void) => Vector3f get_pos do
        return pos;
    end

    finalize with
        _delete_obj(direction);
        _delete_obj(sprite_kill_left);
        _delete_obj(sprite_kill_right);
        _delete_obj(sprite_idle);
    end;

    par/or do
        every gc in e_DRAW do
            if killing then
                if direction.is_left() then
                    _gc_color_draw2(gc, &sprite_kill_left, _toVector3f(pos));
                else
                    _gc_color_draw2(gc, &sprite_kill_right, _toVector3f(pos));
                end
            else
                _gc_color_draw2(gc, &sprite_idle, _toVector3f(pos));
            end
        end
    with
        every e_UPDATE do
            // Only have to check one sprite because they update simultaneously
            if sprite_kill_left.is_finished() then
                killing = false;
            end

            if not killing then
                loop pingu in global:world:get_pingus():all_pingus do
                    if pingu:is_inside((int)(pos.x + 38), (int)(pos.y + 90), (int)(pos.x + 42), (int)(pos.y + 98)) then
                        killing = true;
                        direction = _PTR2REF(pingu:direction);
                        emit pingu:e_CHANGE_ACTION => _A_DEAD;
                        sprite_kill_left.restart();
                        sprite_kill_right.restart();
                        break;
                    end
                end
            end

            if killing then
                // Update both sprites so they finish at the same time.
                sprite_kill_left.update();
                sprite_kill_right.update();

                // original FIXME: Should be a different sound
                if sprite_kill_left.get_current_frame() == 7 then
                    global:world:world:play_sound("splash", _toVector3f(pos));
                end
            else
                sprite_idle.update();
            end
        end
    with
        await e_DELETE;
    end
end

#endif
