#ifndef ENTRANCE_CEU
#define ENTRANCE_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../iworld.ceu"
#include "../worldobj.ceu"
#include "../pingu_holder.ceu"

native/pre do
    ##include "pingus/worldobjs/entrance.hpp"
    ##define WEntrance WorldObjs::Entrance
    ##define ED_MISC WorldObjs::Entrance::EntranceDirection::MISC
    ##define ED_RIGHT WorldObjs::Entrance::EntranceDirection::RIGHT
end

input _World* WORLD_UPDATE;
input _WEntrance* DELETE_ENTRANCE;

class Entrance with
    interface IWorldObj;
    var IWorld& world;
do
    var _WEntrance&? entrance = _PTR2REF((_WEntrance*)me);

    var int last_release = 150 - entrance.release_rate;
    var int last_direction = 0;

    function (void) => bool pingu_ready do
        if (last_release + entrance.release_rate) < entrance.world:get_time() then
            last_release = _PTR2REF(entrance.world:get_time()); //that's not a pointer even
            return true;
        end

        return false;
    end

    par/or do
        every w in WORLD_UPDATE do
            if w != entrance.world then
                continue;
            end

            if pingu_ready() and not entrance.world:check_armageddon() then
                var _PHPackage* package = _new_PHPackage();
                package:entrance = &entrance;
                package:left = true;

                if entrance.direction == _ED_MISC then
                    if last_direction then
                        last_direction = 0;
                    else
                        package:left = false;
                        last_direction = 1;
                    end
                else/if entrance.direction == _ED_RIGHT then
                    package:left = false;
                end

                emit world.get_pingus():e_MAKE_PINGU => package;
            end
        end
    with
        var _WEntrance* e = await DELETE_ENTRANCE until (e == &entrance);
    end
end

#endif
