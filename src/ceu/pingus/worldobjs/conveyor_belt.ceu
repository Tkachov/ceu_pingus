#ifndef CONVEYOR_BELT_CEU
#define CONVEYOR_BELT_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../math/vector3f.ceu"
#include "../iworld.ceu"
#include "../worldobj.ceu"
#include "../ipingu_holder.ceu"
#include "../global_interface.ceu"

native/pre do
    ##include "math/vector3f.hpp"
    ##include "engine/display/scene_context.hpp"
    ##include "pingus/world.hpp"
end

class ConveyorBelt with
    interface IWorldObj;

    var int width;
    var float speed;
    var float counter;
do
    var Sprite left_sur;
    var Sprite right_sur;
    var Sprite middle_sur;
    call/rec left_sur.reload_by_name("worldobjs/conveyorbelt_left");
    call/rec right_sur.reload_by_name("worldobjs/conveyorbelt_right");
    call/rec middle_sur.reload_by_name("worldobjs/conveyorbelt_middle");

    call/rec global:world:add_object(&this);

    function (_FileReader* reader) => void construct do end
    function (void) => float get_z_pos do return pos.z; end
    function (Vector3f p) => void set_pos do pos = p; end
    function (void) => Vector3f get_pos do return pos; end

    par/or do
        every gc in global:draw do
            _gc_color_draw2(gc, left_sur.get(), _toVector3f(pos));

            loop/100 i in width do
                _gc_color_draw2(gc, middle_sur.get(),
                    _Vector3f(
                        (float)(pos.x + (float)(left_sur.get_width() + i*middle_sur.get_width())),
                        (float)pos.y
                    ));
            end

            _gc_color_draw2(gc, right_sur.get(),
                _Vector3f(
                    (float)(pos.x + (float)(left_sur.get_width() + width*middle_sur.get_width())),
                    (float)pos.y
                ));
        end
    with
        every e_STARTUP do
            var _CollisionMask* mask = _new_CollisionMask("worldobjs/conveyorbelt_cmap");

            loop/100 i in width+2 do
                global:world:put(mask, (int)pos.x + 15*i, (int)pos.y, _GT_SOLID);
            end

            _delete_obj(mask);
        end
    with
        every e_UPDATE do
            left_sur.update();
            middle_sur.update();
            right_sur.update();

            loop pingu in global:world:get_pingus():all_pingus do
                if pingu:get_x() > pos.x and pingu:get_x() < pos.x + 15*(float)(width+2) and
                   pingu:get_y() > pos.y-2 and pingu:get_y() < pos.y+10 then
                    var Vector3f ppos;
                    ppos.x = pingu:get_pos().x - speed*0.025;
                    ppos.y = pingu:get_pos().y;
                    ppos.z = pingu:get_pos().z;
                    pingu:set_pos(_toVector3f(ppos));
                end
            end
        end
    with
        await e_DELETE;
    end
end

#endif
