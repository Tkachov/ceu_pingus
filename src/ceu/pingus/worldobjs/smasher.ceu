#ifndef SMASHER_CEU
#define SMASHER_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/sound/sound.ceu"
#include "../../math/math.ceu"
#include "../particles/ismoke_particles_holder.ceu"
#include "../iworld.ceu"
#include "../worldobj.ceu"
#include "../ipingu_holder.ceu"
#include "../pingu_holder_listener.ceu"

class Smasher with
    interface IWorldObj;
    var _Sprite&? sprite;
do
    call/rec global:world:add_object(&this);

    function (_FileReader* reader) => void construct do end;

    function (void) => float get_z_pos do
        return pos.z;
    end

    function (Vector3f p) => void set_pos do
        pos = p;
    end

    function (void) => Vector3f get_pos do
        return pos;
    end

    finalize with
        _delete_obj(sprite);
    end;

    var bool smashing = false;
    var bool downwards = false;
    var int count = 0;

    par/or do
        every e_STARTUP do
            _log_info("Drawing colmap entry");
            var _CollisionMask* mask = _new_CollisionMask("traps/smasher_cmap");
            global:world:put(mask, pos.x, pos.y, _GT_SOLID);
            _delete_obj(mask);
        end
    with
        every gc in e_DRAW do
            _gc_color_draw2(gc, &sprite, _toVector3f(pos));
        end
    with
        every e_UPDATE do
            loop pingu in global:world:get_pingus():all_pingus do
                if pingu:get_current_action() == _A_SPLASHED or smashing or
                   pingu:get_current_action() == _A_DEAD or pingu:get_current_action() == _A_EXITED then
                    continue;
                end

                if not (pingu:direction.is_left() and pingu:get_x() > pos.x+65 and pingu:get_x() < pos.x+85) and
                   not (pingu:direction.is_right() and pingu:get_x() > pos.x+190 and pingu:get_x() < pos.x+210) then
                    continue;
                end

                // Activate the smasher if a Pingu is under it
                count = 0;
                downwards = true;
                smashing = true;
            end

            if not smashing then
                continue;
            end

            sprite.set_frame(count);

            if downwards then
                if count < 5 then
                    count = count + 1;
                else
                    // SMASH!!! The thing hitten earth and kills the pingus
                    downwards = false;
                    count = count - 1;
                    _Sound_PingusSound_play_sound("tenton");

                    loop i in 20 do
                        global:world:get_smoke_particles_holder():add_particle(
                            pos.x + 20 + (float)(_rand()%260),
                            pos.y + 180,
                            _Math__frand()-0.5,
                            _Math__frand()-0.5
                        );
                    end

                    loop pingu in global:world:get_pingus():all_pingus do
                        if pingu:get_current_action() == _A_SPLASHED or pingu:get_current_action() == _A_BOMBER or
                           pingu:get_current_action() == _A_DEAD or pingu:get_current_action() == _A_EXITED then
                            continue;
                        end

                        if pingu:is_inside(pos.x + 30, pos.y + 90, pos.x + 250, pos.y + 190) then
                            emit pingu:e_CHANGE_ACTION => _A_SPLASHED;
                        end
                    end
                end
            else
                if count <= 0 then
                    count = 0;
                    smashing = false;
                else
                    count = count - 1;
                end
            end
        end
    with
        await e_DELETE;
    end
end

#endif
