#ifndef ANGELACTION_CEU
#define ANGELACTION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/scene_context.ceu"
#include "../../engine/display/sprite.ceu"
#include "../iaction.ceu"
#include "../ipingu.ceu"

native/pre do
    ##include "engine/sound/sound.hpp"
    ##define Sound_PingusSound_play_sound Sound::PingusSound::play_sound

    ##include "pingus/globals.hpp"
    ##define globals__game_speed globals::game_speed
    ##define Math__sin Math::sin
end

class AngelAction with
    interface IAction;
    interface IPinguAction;
do    
    pingu.ceu_action = &this;

    function (int a) => bool change_allowed do
        return true;
    end

    catchable = true;

    function (void) => Vector3f get_center_pos do
        var Vector3f result;
        result.x = pingu.me.get_pos().x;
        result.y = pingu.me.get_pos().y - 16;
        result.z = pingu.me.get_pos().z;
        return result;
    end

    function (void) => _const_char_string get_name do
        return _get_screenname(_A_ANGEL);
    end

    var _Sprite&? sprite = _PTR2REF(_new_SpriteA(_scc3("pingus/player", pingu.me.get_owner_str(), "/angel")));
    finalize with _delete_Sprite(sprite); end;

    var float counter = 0;
    var float x_pos = pingu.me.get_x();

    par do
        every pingu.e_UPDATE_CALLED do
            sprite.update();

            counter = counter + (float)_globals__game_speed;
            //pingu.me.set_pos(x_pos + 20*_Math__sin(counter*3.0), pingu.me.get_y() - 50.0*0.025);
            pingu.me.set_pos(x_pos + 4.0*_Math__sin(counter*0.01), (float)(pingu.me.get_y() - 40.0*0.025));

            // Out of screen, let the pingu die
            if pingu.me.get_y() < -32 then
                escape _A_DEAD;
            end
        end
    with
        every gc in pingu.e_DRAW_CALLED do
            _gc_color_draw2(gc, sprite, pingu.me.get_pos());
        end
    end
end

#endif
