#ifndef BOARDERACTION_CEU
#define BOARDERACTION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/scene_context.ceu"
#include "../../math/vector3f.ceu"
#include "../iaction.ceu"
#include "../ipingu.ceu"
#include "../state_sprite.ceu"

class BoarderAction with
    interface IAction;
    interface IPinguAction;
do
    pingu.ceu_action = &this;

    function (int a) => bool change_allowed do
        return true;
    end

    catchable = true;

    function @rec (void) => Vector3f get_center_pos do
        var Vector3f result;
        result.x = pingu.get_pos().x;
        result.y = pingu.get_pos().y - 16;
        result.z = pingu.get_pos().z;
        return result;
    end

    function (void) => _const_char_string get_name do
        return _get_screenname(_A_BOARDER);
    end

    var StateSprite sprite;
    load_file_directions(sprite, "/boarder/");

    var float speed = 0;

    par do
        every gc in pingu.e_DRAW_CALLED do
            _gc_color_draw2(gc, sprite.get(pingu.direction), pingu.get_pos());
        end
    with
        every global:e_PINGU_UPDATE_ALL do
            sprite.get(pingu.direction):update();

            //not on ground
            if not pingu.rel_getpixel(0, -1) and not pingu.rel_getpixel(0, -2) then
                var Vector3f f;
                f.x = speed*(float)(pingu.direction);
                f.y = 0;
                f.z = 0;
                pingu.apply_force(f);
                escape _A_WALKER;
            end

            if speed < 15.0 then
                speed = speed + 15.0 * 0.025;
            else
                speed = 15.0;
            end

            // Incremental update so that we don't skip pixels
            var Vector3f new_pos;
            new_pos.x = pingu.get_pos().x + (float)(pingu.direction)*speed;
            new_pos.y = pingu.get_pos().y;

            loop/100 do
                if (int)new_pos.x == (int)pingu.get_pos().x then
                    break;
                end

                var Vector3f old_pos;
                old_pos.x = pingu.get_pos().x;
                old_pos.y = pingu.get_pos().y;

                var float offset = 1;
                if old_pos.x >= new_pos.x then
                    offset = -1;
                end;

                pingu.set_pos(_Vector3f(old_pos.x + offset, old_pos.y, old_pos.z));

                if pingu.rel_getpixel(1, 0) then
                    // Hit a wall
                    pingu.set_pos(_toVector3f(old_pos));

                    var Vector3f f;
                    f.x = (float)(speed*(float)(pingu.direction)*0.5);
                    f.y = -(float)(speed*(float)(_abs((int)pingu.direction))*0.5);
                    f.z = 0;
                    pingu.apply_force(f);
                    escape _A_WALKER;
                end
            end
        end
    end
end

#endif
