#ifndef SPLASHEDACTION_CEU
#define SPLASHEDACTION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/scene_context.ceu"
#include "../../engine/display/sprite.ceu"
#include "../../math/vector3f.ceu"
#include "../iaction.ceu"
#include "../ipingu.ceu"

native/pre do
    ##include "pingus/worldobj.hpp"
    ##define WorldObj__get_world WorldObj::get_world
end

native @pure _WorldObj__get_world();

class SplashedAction with
    interface IAction;
    interface IPinguAction;
do
    var _PinguAction&? splashed = _PTR2REF(pingu.set_ptr(_A_SPLASHED));
    pingu.ceu_action = &this;

    function (int a) => bool change_allowed do
        return false;
    end

    catchable = false;

    var _Sprite&? sprite = _PTR2REF(_new_SpriteA(_scc3("pingus/player", splashed.pingu:get_owner_str(), "/splat")));
    finalize with _delete_Sprite(sprite); end;

    par do
        every gc in pingu.e_DRAW_CALLED do
            _gc_color_draw2(gc, sprite, splashed.pingu:get_pos());
        end
    with
        every pingu.e_UPDATE_CALLED do
            sprite.update();

            if sprite.is_finished() then
                escape _A_DEAD;
            end
        end
    with
        await pingu.e_UPDATE_CALLED;
        _WorldObj__get_world():play_sound("splash", splashed.pingu:get_pos());
    end
end

#endif
