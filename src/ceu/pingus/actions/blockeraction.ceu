#ifndef BLOCKERACTION_CEU
#define BLOCKERACTION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/scene_context.ceu"
#include "../iaction.ceu"
#include "../ipingu.ceu"
#include "../pingu_action.ceu"
#include "../state_sprite.ceu"
#include "../pingu_holder_listener.ceu"

class BlockerAction with
    interface IAction;
    interface IPinguAction;
do
    var _PinguAction&? blocker = _PTR2REF(pingu.set_ptr(_A_BLOCKER));
    pingu.ceu_action = &this;

    function (int a) => bool change_allowed do
        return true;
    end

    catchable = true;

    function (void) => Vector3f get_center_pos do
        var Vector3f result;
        result.x = pingu.me.get_pos().x;
        result.y = pingu.me.get_pos().y - 16;
        result.z = pingu.me.get_pos().z;
        return result;
    end

    var StateSprite sprite;
    load_file_directions(sprite, blocker, "/blocker/");

    if pingu.rel_getpixel(0, -1) == _GT_NOTHING then
        if pingu.rel_getpixel(0, -2) == _GT_GROUND then
            pingu.me.set_y(pingu.me.get_y() + 1);
        else/if pingu.rel_getpixel(0, -2) == _GT_NOTHING and pingu.rel_getpixel(0, -3) == _GT_GROUND then
            pingu.me.set_y(pingu.me.get_y() + 2);
        end
    end

    par do
        every gc in pingu.e_DRAW_CALLED do
            _gc_color_draw2(gc, sprite.get(pingu.me.direction), pingu.me.get_pos());
        end
    with
        every pingu.e_UPDATE_CALLED do
            if pingu.rel_getpixel(0,-1) == _GT_NOTHING then
                escape _A_FALLER;
            end

            loop catched in global:world:get_pingus():all_pingus do
                // avoid 'self' catch
                if catched == &pingu then
                    continue;
                end

                if catched:me.get_x() > pingu.me.get_x()-16 and
                   catched:me.get_x() < pingu.me.get_x()+16 and
                   catched:me.get_y() > pingu.me.get_y()-32 and
                   catched:me.get_y() < pingu.me.get_y()+5 then
                    if catched:me.get_x() > pingu.me.get_x() then
                        catched:me.direction.right();
                    else
                        catched:me.direction.left();
                    end
                end
            end

            sprite.update();
        end
    end
end

#endif
