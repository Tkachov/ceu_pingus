#ifndef FLOATERACTION_CEU
#define FLOATERACTION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/scene_context.ceu"
#include "../../engine/display/sprite.ceu"
#include "../../math/vector3f.ceu"
#include "../iaction.ceu"
#include "../ipingu.ceu"

class FloaterAction with
    interface IAction;
    interface IPinguAction;
do
    pingu.ceu_action = &this;

    function (int a) => bool change_allowed do
        return (a == _A_BOMBER);
    end

    catchable = true;

    function (void) => Vector3f get_center_pos do
        var Vector3f result;
        result.x = pingu.me.get_pos().x;
        result.y = pingu.me.get_pos().y - 16;
        result.z = pingu.me.get_pos().z;
        return result;
    end

    function (void) => _const_char_string get_name do
        return _get_screenname(_A_FLOATER);
    end

    var _Sprite&? sprite = _new_SpriteA(_scc3("pingus/player", pingu.me.get_owner_str(), "/floater/left"));
    finalize with _delete_Sprite(sprite); end;

    par do
        every gc in pingu.e_DRAW_CALLED do
            _gc_color_draw2(gc, sprite, pingu.me.get_pos());
        end
    with
        every pingu.e_UPDATE_CALLED do
            sprite.update();
            pingu.me.set_velocity(_Vector3f(0.0, 1.0));

            if pingu.rel_getpixel(0, -1) == _GT_NOTHING then
                pingu.me.set_y(pingu.me.get_y() + 1);
            else
                escape _A_WALKER;
            end
        end
    end
end

#endif
