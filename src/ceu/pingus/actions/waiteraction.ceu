#ifndef WAITERACTION_CEU
#define WAITERACTION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/scene_context.ceu"
#include "../../engine/display/sprite.ceu"
#include "../iaction.ceu"
#include "../ipingu.ceu"

class WaiterAction with
    interface IAction;
    interface IPinguAction;
do    
    pingu.ceu_action = &this;

    function (int a) => bool change_allowed do
        return true;
    end

    catchable = true;

    function @rec (void) => Vector3f get_center_pos do
        var Vector3f result;
        result.x = pingu.get_pos().x;
        result.y = pingu.get_pos().y - 16;
        result.z = pingu.get_pos().z;
        return result;
    end

    function (void) => _const_char_string get_name do
        return _get_screenname(_A_WAITER);
    end

    var _Sprite&? sprite = _PTR2REF(_new_SpriteA(_scc3("pingus/player", pingu.me.get_owner_str(), "/waiter/left")));
    finalize with _delete_Sprite(sprite); end;

    par do
        every pingu.e_UPDATE_CALLED do
              sprite.update();
        end
    with
        every gc in pingu.e_DRAW_CALLED do
            _gc_color_draw2(gc, sprite, pingu.get_pos());
        end
    with
        //in C++ there was a countdown from 2 to 0 with step 0.025 per frame - that's 80 frames
        loop i in 80 do
            await pingu.e_UPDATE_CALLED;
        end
        escape _A_WALKER;
    end
end

#endif
