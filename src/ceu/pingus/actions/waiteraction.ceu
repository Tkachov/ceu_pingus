#ifndef WAITERACTION_CEU
#define WAITERACTION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/scene_context.ceu"
#include "../../engine/display/sprite.ceu"
#include "../iaction.ceu"
#include "../ipingu.ceu"

class WaiterAction with
    interface IAction;
    interface IPinguAction;
do
    var _PinguAction&? waiter = _PTR2REF(pingu.set_ptr(_A_WAITER));
    pingu.ceu_action = &this;

    function (int a) => bool change_allowed do
        return true;
    end

    catchable = true;

    function (void) => Vector3f get_center_pos do
        var Vector3f result;
        result.x = pingu.me.get_pos().x;
        result.y = pingu.me.get_pos().y - 16;
        result.z = pingu.me.get_pos().z;
        return result;
    end

    var _Sprite&? sprite = _PTR2REF(_new_SpriteA(_scc3("pingus/player", pingu.me.get_owner_str(), "/waiter/left")));
    finalize with _delete_Sprite(sprite); end;

    par do
        every pingu.e_UPDATE_CALLED do
              sprite.update();
        end
    with
        every gc in pingu.e_DRAW_CALLED do
            _gc_color_draw2(gc, sprite, pingu.me.get_pos());
        end
    with
        //in C++ there was a countdown from 2 to 0 with step 0.025 per frame - that's 80 frames
        loop i in 80 do
            await pingu.e_UPDATE_CALLED;
        end
        escape _A_WALKER;
    end
end

#endif
