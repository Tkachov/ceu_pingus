#ifndef JUMPERACTION_CEU
#define JUMPERACTION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/scene_context.ceu"
#include "../iaction.ceu"
#include "../ipingu.ceu"
#include "../state_sprite.ceu"

class JumperAction with
    interface IAction;
    interface IPinguAction;
do
    pingu.ceu_action = &this;

    function (int a) => bool change_allowed do
        return true;
    end

    catchable = true;

    function (void) => Vector3f get_center_pos do
        var Vector3f result;
        result.x = pingu.me.get_pos().x;
        result.y = pingu.me.get_pos().y - 16;
        result.z = pingu.me.get_pos().z;
        return result;
    end

    function (void) => _const_char_string get_name do
        return _get_screenname(_A_JUMPER);
    end

    var StateSprite sprite;
    load_file_directions(sprite, "/jumper/");

    par do
        every gc in pingu.e_DRAW_CALLED do
            _gc_color_draw2(gc, sprite.get(pingu.me.direction), pingu.me.get_pos());
        end
    with
        await pingu.e_UPDATE_CALLED;

        // if climber, do a wall-jump, else just jump forward
        if pingu.get_previous_action() == _A_CLIMBER then
            pingu.me.direction.change();
        end

        var float y_velocity = -4.58;
        var float x_velocity = y_velocity; //left

        if pingu.me.direction.is_right() then
            x_velocity = x_velocity * -1;
        end

        pingu.me.set_velocity(pingu.me.get_velocity() + _Vector3f(x_velocity, y_velocity));

        // Move the pingu in the air, so that it can start 'falling'
        pingu.me.set_y(pingu.me.get_y() - 1);

        escape _A_FALLER;
    end
end

#endif
