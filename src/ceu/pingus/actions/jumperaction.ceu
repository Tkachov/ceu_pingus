#ifndef JUMPERACTION_CEU
#define JUMPERACTION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/scene_context.ceu"
#include "../iaction.ceu"
#include "../ipingu.ceu"
#include "../state_sprite.ceu"

class JumperAction with
    interface IAction;
    interface IPinguAction;
do
    var _PinguAction&? jumper = _PTR2REF(pingu.set_ptr(_A_JUMPER));
    pingu.ceu_action = &this;

    function (int a) => bool change_allowed do
        return true;
    end

    catchable = true;

    function (void) => Vector3f get_center_pos do
        var Vector3f result;
        result.x = pingu.me.get_pos().x;
        result.y = pingu.me.get_pos().y - 16;
        result.z = pingu.me.get_pos().z;
        return result;
    end

    var StateSprite sprite;
    load_file_directions(sprite, jumper, "/jumper/");

    par do
        every gc in pingu.e_DRAW_CALLED do
            _gc_color_draw2(gc, sprite.get(jumper.pingu:direction), jumper.pingu:get_pos());
        end
    with
        await pingu.e_UPDATE_CALLED;

        // if climber, do a wall-jump, else just jump forward
        if pingu.get_previous_action() == _A_CLIMBER then
            jumper.pingu:direction.change();
        end

        var float y_velocity = -4.58;
        var float x_velocity = y_velocity; //left

        if jumper.pingu:direction.is_right() then
            x_velocity = x_velocity * -1;
        end

        jumper.pingu:set_velocity(jumper.pingu:get_velocity() + _Vector3f(x_velocity, y_velocity));

        // Move the pingu in the air, so that it can start 'falling'
        jumper.pingu:set_y(jumper.pingu:get_y() - 1);

        escape _A_FALLER;
    end
end

#endif
