#ifndef SLIDERACTION_CEU
#define SLIDERACTION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/scene_context.ceu"
#include "../../math/vector3f.ceu"
#include "../iaction.ceu"
#include "../ipingu.ceu"
#include "../right_left_sprite.ceu"

class SliderAction with
    interface IAction;
    interface IPinguAction;
    interface IPosGiver;
do
    pingu.ceu_action = &this;

    function (int a) => bool change_allowed do
        return true;
    end

    catchable = true;

    function @rec (void) => Vector3f get_center_pos do
        var Vector3f result = (call/rec pingu.get_pos());
        result.y = result.y - 16;
        return result;
    end

    function (void) => _const_char_string get_name do
        return _get_screenname(_A_SLIDER);
    end

    function @rec (void) => Vector3f get_pos do
        var Vector3f res = (call/rec pingu.get_pos());
        res.y = res.y - 2;
        return res;
    end

    var RightLeftSprite sprite with
        this.pingu = pingu;
        this.pos_giver = outer;
        this.left_name = _new_String(_concat2(pingu.get_owner_str(), "/slider/left"));
        this.right_name = _new_String(_concat2(pingu.get_owner_str(), "/slider/right"));
    end;

    var float speed = 10;

    loop do
        await global:world:update;

        loop/100 i in speed do
            if (call/rec pingu.rel_getpixel(1, 0)) != _GT_NOTHING then
                break;
            end

            pingu.set_x(pingu.get_x() + (float)pingu.direction().value);

            if (call/rec pingu.rel_getpixel(0, -1)) == _GT_NOTHING then
                if speed > 5 then
                    speed = 5;
                end

                var float x_speed = speed;
                if not pingu.direction().is_right() then
                    x_speed = -speed;
                end

                var Vector3f newvel = pingu.get_velocity();
                newvel.x = newvel.x + x_speed;
                pingu.set_velocity(newvel);

                escape _A_FALLER;
            end
        end

        speed = speed - 7*0.025;

        if speed < 1 then
            escape _A_WALKER;
        end
    end
end

#endif
