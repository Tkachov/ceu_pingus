#ifndef BOMBERACTION_CEU
#define BOMBERACTION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/scene_context.ceu"
#include "../../math/vector3f.ceu"
#include "../iaction.ceu"
#include "../ipingu.ceu"
#include "../state_sprite.ceu"

native/pre do
    ##include "pingus/actions/bomber.hpp"
    ##define ABomber Actions::Bomber

    ##include "pingus/particles/pingu_particle_holder.hpp"
    ##include "pingus/colliders/pingu_collider.hpp"
    ##include "pingus/movers/linear_mover.hpp"
    ##include "pingus/pingu_enums.hpp"
    ##include "pingus/worldobj.hpp"
    ##define WorldObj__get_world WorldObj::get_world
    ##define WorldObj__get_world_remove WorldObj::get_world()->remove
    ##define WorldObj__get_world_play_sound WorldObj::get_world()->play_sound
    ##define Colliders__PinguCollider Colliders::PinguCollider
    ##define Math__abs Math::abs
end

native @pure _Colliders__PinguCollider(),
             _WorldObj__get_world(),
             _WorldObj__get_world_remove(),
             _WorldObj__get_world_play_sound();

class BomberAction with
    interface IAction;
do
    var _ABomber&? bomber = _PTR2REF((_ABomber*)pingu.set_ptr(_A_BOMBER));

    load_directions(bomber.sprite, bomber, "/bomber/");
    _WorldObj__get_world_play_sound("ohno", bomber.pingu:get_pos());

    par do
        every pingu.e_UPDATE_CALLED do
            bomber.sprite.update();

            // Move the Pingu
            bomber.mover.reset(bomber.pingu:get_pos());
            bomber.mover.update(bomber.pingu:get_velocity(), _Colliders__PinguCollider(_pingu_height));
            bomber.pingu:set_pos(bomber.mover.get_pos());
        end
    with
        par do
            var _SceneContext* gc = await pingu.e_DRAW_CALLED until bomber.sprite[bomber.pingu:direction].get_current_frame() >= 13;
            _gc_color_draw(gc, bomber.explo_surf, _Vector3f(bomber.pingu:get_x()-32, bomber.pingu:get_y()-48));
        with
            every gc in pingu.e_DRAW_CALLED do
                _gc_color_draw(gc, bomber.sprite[bomber.pingu:direction], bomber.pingu:get_pos());
            end
        end
    with
        // If the Bomber hasn't 'exploded' yet...
        every pingu.e_UPDATE_CALLED do
            if bomber.sprite[bomber.pingu:direction].get_current_frame() > 9 then
                break;
            end

            // ...and it has hit Water or Lava
            if bomber.rel_getpixel(0, -1) == _GT_WATER or bomber.rel_getpixel(0, -1) == _GT_LAVA then
                escape _A_DROWN;
            end

            // ...and it has hit the ground too quickly
            if bomber.rel_getpixel(0, -1) != _GT_NOTHING and bomber.pingu:get_velocity().y > _deadly_velocity then
                escape _A_SPLASHED;
            end
        end

        //Play sound at 10th frame (>9)
        _WorldObj__get_world():play_sound("plop", bomber.pingu:get_pos());

        //Wait for 13th frame to throw particles
        await pingu.e_UPDATE_CALLED until (bomber.sprite[bomber.pingu:direction].get_current_frame() > 12);

        // Throwing particles
        _WorldObj__get_world():get_pingu_particle_holder():add_particle(
            (int)bomber.pingu:get_x(), (int)bomber.pingu:get_y() - 5
        );

        //Wait for 13th frame again?..
        PRE_CONDITION_AWAIT(pingu.e_UPDATE_CALLED, bomber.sprite[bomber.pingu:direction].get_current_frame() >= 13);

        _WorldObj__get_world_remove(
            bomber.bomber_radius,
            (int)((int)bomber.pingu:get_x() - (bomber.bomber_radius.get_width()/2)),
            (int)((int)bomber.pingu:get_y() - 16 - (bomber.bomber_radius.get_width()/2))
        );

        // The pingu explode
        PRE_CONDITION_AWAIT(pingu.e_UPDATE_CALLED, bomber.sprite[bomber.pingu:direction].is_finished());

        //Be dead forever
        loop do
            bomber.pingu:set_status(_PS_DEAD);
            await pingu.e_UPDATE_CALLED;
        end
    end
end

#endif
