#ifndef DROWNACTION_CEU
#define DROWNACTION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/scene_context.ceu"
#include "../iaction.ceu"
#include "../ipingu.ceu"
#include "../state_sprite.ceu"

class DrownAction with
    interface IAction;
    interface IPinguAction;
do
    var _PinguAction&? drown = _PTR2REF(pingu.set_ptr(_A_DROWN));
    pingu.ceu_action = &this;

    function (int a) => bool change_allowed do
        return true;
    end

    catchable = false;

    var StateSprite sprite;
    load_file_directions(sprite, drown, "/drownfall/");

    par do
        every gc in pingu.e_DRAW_CALLED do
            _gc_color_draw2(gc, sprite.get(drown.pingu:direction), drown.pingu:get_pos());
        end
    with
        every pingu.e_UPDATE_CALLED do
            sprite.get(drown.pingu:direction):update();

            if sprite.get(drown.pingu:direction):is_finished() then
                escape _A_DEAD;
            end
        end
    end
end

#endif
