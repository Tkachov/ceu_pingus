#ifndef EXITERACTION_CEU
#define EXITERACTION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/scene_context.ceu"
#include "../../engine/sound/sound.ceu"
#include "../iaction.ceu"
#include "../ipingu.ceu"
#include "../state_sprite.ceu"

class ExiterAction with
    interface IAction;
    interface IPinguAction;
do
    var _PinguAction&? exiter = _PTR2REF(pingu.set_ptr(_A_EXITER));
    pingu.ceu_action = &this;

    function (int a) => bool change_allowed do
        return true;
    end

    catchable = true;

    function (void) => Vector3f get_center_pos do
        var Vector3f result;
        result.x = pingu.me.get_pos().x;
        result.y = pingu.me.get_pos().y - 16;
        result.z = pingu.me.get_pos().z;
        return result;
    end

    var StateSprite sprite;
    load_file_directions(sprite, exiter, "/exit/");

    par do
        every pingu.e_UPDATE_CALLED do
            sprite.get(pingu.me.direction):update();

            if sprite.get(pingu.me.direction):is_finished() then
                escape _A_EXITED;
            end
        end
    with
        every gc in pingu.e_DRAW_CALLED do
            _gc_color_draw2(gc, sprite.get(pingu.me.direction), pingu.me.get_pos());
        end
    with
        await pingu.e_UPDATE_CALLED;
        _Sound_PingusSound_play_sound("yipee");
    end
end

#endif
