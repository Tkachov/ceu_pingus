#ifndef EXITERACTION_CEU
#define EXITERACTION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/scene_context.ceu"
#include "../../engine/sound/sound.ceu"
#include "../iaction.ceu"
#include "../ipingu.ceu"
#include "../state_sprite.ceu"

class ExiterAction with
    interface IAction;
    interface IPinguAction;
do
    pingu.ceu_action = &this;

    function (int a) => bool change_allowed do
        return true;
    end

    catchable = true;

    function @rec (void) => Vector3f get_center_pos do
        var Vector3f result = pingu.get_pos();
        result.y = result.y - 16;
        return result;
    end

    function (void) => _const_char_string get_name do
        return _get_screenname(_A_EXITER);
    end

    var StateSprite sprite;
    load_file_directions(sprite, "/exit/");

    par do
        every global:e_PINGU_UPDATE_ALL do
            sprite.get(pingu.direction().value):update();

            if sprite.get(pingu.direction().value):is_finished() then
                escape _A_EXITED;
            end
        end
    with
        every gc in pingu.e_DRAW_CALLED do
            _gc_color_draw2(gc, sprite.get(pingu.direction().value), _toVector3f(pingu.get_pos()));
        end
    with
        await global:e_PINGU_UPDATE_ALL;
        _Sound_PingusSound_play_sound("yipee");
    end
end

#endif
