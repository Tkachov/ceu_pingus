#ifndef EXITERACTION_CEU
#define EXITERACTION_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/scene_context.ceu"
#include "../iaction.ceu"
#include "../ipingu.ceu"
#include "../state_sprite.ceu"

native/pre do
    ##include "pingus/actions/exiter.hpp"
    ##define AExiter Actions::Exiter

    ##include "engine/sound/sound.hpp"
    ##define Sound_PingusSound_play_sound Sound::PingusSound::play_sound
end

class ExiterAction with
    interface IAction;
do
    var _AExiter&? exiter = _PTR2REF((_AExiter*)pingu.set_ptr(_A_EXITER));
    load_directions(exiter, "/exit/");

    par/and do
        every pingu.e_UPDATE_CALLED do
            exiter.sprite[exiter.pingu:direction].update();

            if exiter.sprite[exiter.pingu:direction].is_finished() then
                if exiter.pingu:get_status() != _PS_EXITED then
                    exiter.pingu:set_status(_PS_EXITED);
                end
            end
        end
    with
        every gc in pingu.e_DRAW_CALLED do
            _gc_color_draw(gc, exiter.sprite[exiter.pingu:direction], exiter.pingu:get_pos());
        end
    with
        await pingu.e_UPDATE_CALLED;
        _Sound_PingusSound_play_sound("yipee");
    end
end

#endif
