#ifndef MENUBUTTON_CEU
#define MENUBUTTON_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/gui/igui_component.ceu"
#include "../../proxies/pointerproxy.ceu"

native/pre do
    ##include "engine/sound/sound.hpp"
    ##include "pingus/components/menu_button.hpp"
    ##include "pingus/screens/pingus_menu.hpp"

    ##define Sound_PingusSound_play_sound Sound::PingusSound::play_sound
end

class MenuButton with
    interface IGUI_Component;
do
    var _MenuButton&? me_ = _PTR2REF((_MenuButton*)&this.me);
    var PointerProxy p with
        this.gui = outer;
    end;

    loop do
        await mouse_enter;
        me_.mouse_over = true;
        _Sound_PingusSound_play_sound("tick");
        me_.menu:set_hint(me_.desc) finalize with end;

        await mouse_leave;
        me_.mouse_over = false;
        me_.menu:set_hint("");
    end
end

#endif
