#ifndef PAUSEBUTTON_CEU
#define PAUSEBUTTON_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/sprite.ceu"
#include "../../engine/gui/igui_component.ceu"
#include "../../proxies/eventproxy.ceu"
#include "../../proxies/primarybuttonproxy.ceu"
#include "../screens/igame_session.ceu"

native/pre do    
    ##define gc_draw(gc, s, rect) gc->draw(*s, Vector2i(rect.left, rect.top))
end

native @pure _gc_draw();

class PauseButton with
    interface IGUI_Component;
    var IGameSession& session;
do
    var Sprite surface;
    var Sprite background;
    var Sprite backgroundhl;
    call/rec background.reload_by_name("core/buttons/hbuttonbgb");
    call/rec backgroundhl.reload_by_name("core/buttons/hbuttonbg");
    call/rec surface.reload_by_name("core/buttons/pause");

    var _GUI_RectComponent&? me_ = _PTR2REF((_GUI_RectComponent*)&this.me);
    var PrimaryButtonProxy p with this.gui = outer; end;
    var EventProxy ep with this.gui = outer; end;

    function @rec (Vector2i pos) => bool is_at_func do
        return false; //TODO
    end

    //freed in GroupComponent
    //finalize with _delete_obj(me_); end

    par do
        every mouse_click do
            session.set_pause(not session.get_pause());
        end
    with
        every gc in draw do
            if session.get_pause() then
                _gc_draw(gc, backgroundhl.get(), me_.rect);
            else
                _gc_draw(gc, background.get(), me_.rect);
            end

            _gc_draw(gc, surface.get(), me_.rect);
        end
    end
end

#endif
