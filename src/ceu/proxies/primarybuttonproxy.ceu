#ifndef PRIMARYBUTTONPROXY_CEU
#define PRIMARYBUTTONPROXY_CEU

#include "proxyeventlist.ceu"

#include "../c.ceu"
#include "../engine/gui/igui_component.ceu"

native/pre do
    ##include "engine/gui/component.hpp"
    ##define GUI_Component GUI::Component
end

class PrimaryButtonProxy with
    var IGUI_Component& gui;
do
    par do
        every g in ON_PRIMARY_BUTTON_PRESS do
            if g == &gui.me then
                emit gui.mouse_press;
            end
        end
    with
        every g in ON_PRIMARY_BUTTON_RELEASE do
            if g == &gui.me then
                emit gui.mouse_release;
            end
        end
    with
        every g in ON_PRIMARY_BUTTON_CLICK do
            if g == &gui.me then
                emit gui.mouse_click;
            end
        end
    with
        loop do
            await gui.mouse_click;
            watching 1s do
                await gui.mouse_click;
                emit gui.mouse_double_click;
            end
        end
    end
end

#endif
